GimelSec

medium

# `setPayoutScheduleFixed`, `setPayoutSchedule` Unable to resize to fewer tiers

## Summary

`setPayoutScheduleFixed`, `setPayoutSchedule` Unable to resize to fewer tiers. Users are unable to resize to fewer tiers than previously.

## Vulnerability Detail

In `setPayoutScheduleFixed` and `setPayoutSchedule`:
```solidity
        // Resize metadata arrays and copy current members to new array
        // NOTE: If resizing to fewer tiers than previously, the final indexes will be removed
        string[] memory newTierWinners = new string[](payoutSchedule.length);
        bool[] memory newInvoiceComplete = new bool[](payoutSchedule.length);
        bool[] memory newSupportingDocumentsCompleted = new bool[](
            payoutSchedule.length
        );

        for (uint256 i = 0; i < tierWinners.length; i++) {
            newTierWinners[i] = tierWinners[i];
        }
        tierWinners = newTierWinners;

        for (uint256 i = 0; i < invoiceComplete.length; i++) {
            newInvoiceComplete[i] = invoiceComplete[i];
        }
        invoiceComplete = newInvoiceComplete;

        for (uint256 i = 0; i < supportingDocumentsComplete.length; i++) {
            newSupportingDocumentsCompleted[i] = supportingDocumentsComplete[i];
        }
        supportingDocumentsComplete = newSupportingDocumentsCompleted;
```

The comment shows that `If resizing to fewer tiers than previously, the final indexes will be removed`. But if users try to resize to fewer tiers than previously, the transaction will be reverted due to `Array accessed at an out-of-bounds or negative index`.

## Impact

Users are unable to resize to fewer tiers than previously.

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredFixedBountyV1.sol#L138
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredPercentageBountyV1.sol#L141

## Tool used

Manual Review

## Recommendation

Fix the for loop:

```diff
        for (uint256 i = 0; i < tierWinners.length; i++) {
+           if (i >= newTierWinners.length) break;
            newTierWinners[i] = tierWinners[i];
        }
        tierWinners = newTierWinners;

        for (uint256 i = 0; i < invoiceComplete.length; i++) {
+           if (i >= newInvoiceComplete.length) break;
            newInvoiceComplete[i] = invoiceComplete[i];
        }
        invoiceComplete = newInvoiceComplete;

        for (uint256 i = 0; i < supportingDocumentsComplete.length; i++) {
+           if (i >= newSupportingDocumentsCompleted.length) break;
            newSupportingDocumentsCompleted[i] = supportingDocumentsComplete[i];
        }
        supportingDocumentsComplete = newSupportingDocumentsCompleted;
```
