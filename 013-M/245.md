clems4ever

medium

# Modify percentages of TieredPercentageBounty must not be possible after first claim happened

## Summary

`setPayoutSchedule` is callable at any time by the issuer to update the schedules. However, this should not be possible to do once at least one payout has been made otherwise this would totally unbalance the percentage strategy.

## Vulnerability Detail

Modifying the percentages after any tier get paid might completely unbalance the rewards and make people happier or less happy depending on whether the new strategy benefits to them or reduces their potential gains.

This is due to the fact that the following function is callable whatever the state of the contract (open or not and whatever if there was a tier already paid out).

```solidity
function setPayoutSchedule(
        string calldata _bountyId,
        uint256[] calldata _payoutSchedule
    ) external onlyProxy {
        IBounty bounty = getBounty(_bountyId);

        require(msg.sender == bounty.issuer(), Errors.CALLER_NOT_ISSUER);

        bounty.setPayoutSchedule(_payoutSchedule);

        emit PayoutScheduleSet(
            address(bounty),
            address(0),
            _payoutSchedule,
            bounty.bountyType(),
            new bytes(0),
            VERSION_1
        );
    }
```
which calls the following function which has no check to verify whether the contract is closed or not and whether any payment has already been made or not.

```solidity
function setPayoutSchedule(uint256[] calldata _payoutSchedule)
        external
        onlyOpenQ
    {
        require(
            bountyType == OpenQDefinitions.TIERED_PERCENTAGE,
            Errors.NOT_A_TIERED_BOUNTY
        );
        uint256 sum;
        for (uint256 i = 0; i < _payoutSchedule.length; i++) {
            sum += _payoutSchedule[i];
        }
        require(sum == 100, Errors.PAYOUT_SCHEDULE_MUST_ADD_TO_100);

        payoutSchedule = _payoutSchedule;

        // Resize metadata arrays and copy current members to new array
        // NOTE: If resizing to fewer tiers than previously, the final indexes will be removed
        string[] memory newTierWinners = new string[](payoutSchedule.length);
        bool[] memory newInvoiceComplete = new bool[](payoutSchedule.length);
        bool[] memory newSupportingDocumentsCompleted = new bool[](
            payoutSchedule.length
        );

        for (uint256 i = 0; i < tierWinners.length; i++) {
            newTierWinners[i] = tierWinners[i];
        }
        tierWinners = newTierWinners;

        for (uint256 i = 0; i < invoiceComplete.length; i++) {
            newInvoiceComplete[i] = invoiceComplete[i];
        }
        invoiceComplete = newInvoiceComplete;

        for (uint256 i = 0; i < supportingDocumentsComplete.length; i++) {
            newSupportingDocumentsCompleted[i] = supportingDocumentsComplete[i];
        }
        supportingDocumentsComplete = newSupportingDocumentsCompleted;
    }
```

This is an issue in two ways because this would totally break the distribution if a tier get paid and the schedule is updated before the rest of the tiers get paid. And also, allowing the issuer to modify the payout would force the developers to trust the issuer of the bounty to not modify the schedule once the dev have started.

## Impact

Unhappy developers who were eventually paid less than what they expected when they started developing for the issue

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredPercentageBountyV1.sol#L141

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/OpenQ/Implementations/OpenQV1.sol#L265

## Tool used

Manual Review

## Recommendation

Ideally, I would freeze the schedule before the bounty gets advertised to developers so that they can trust the amount they will receive in exchange of their work but if that's not possible at least I think it would be better to compute the claimable amounts when closing the competition so that from this point up until actual claims, the amounts won't change.

You can compute the claimableBalances in the closeCompetition function as follows

```solidity
function closeCompetition() external onlyClaimManager {
        require(
            status == OpenQDefinitions.OPEN,
            Errors.CONTRACT_ALREADY_CLOSED
        );

        status = OpenQDefinitions.CLOSED;
        bountyClosedTime = block.timestamp;

        for (uint256 i = 0; i < getTokenAddresses().length; i++) {
            address _tokenAddress = getTokenAddresses()[i];
            fundingTotals[_tokenAddress] = getTokenBalance(_tokenAddress);
            for (uint256 i = 0; i < payoutSchedule.length; i++) {
                claimableBalances[_tokenAddress][i] = (payoutSchedule[i] * fundingTotals[_tokenAddress]) / 100;
            }
        }
    }
```

and claim as follows

```solidity
function claimTiered(
        address _payoutAddress,
        uint256 _tier,
        address _tokenAddress
    ) external onlyClaimManager nonReentrant returns (uint256) {
        require(
            bountyType == OpenQDefinitions.TIERED_PERCENTAGE,
            Errors.NOT_A_TIERED_BOUNTY
        );
        require(!tierClaimed[_tier], Errors.TIER_ALREADY_CLAIMED);

        uint256 claimedBalance = claimableBalances[_tokenAddress][_tier];

        _transferToken(_tokenAddress, claimedBalance, _payoutAddress);
        return claimedBalance;
    }
```