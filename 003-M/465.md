HonorLt

medium

# Impossible to reduce the size of payout schedule

## Summary
Reducing the size of the `payoutSchedule` array will throw a runtime index out-of-bounds exception.

## Vulnerability Detail
`setPayoutScheduleFixed` allows adjusting the `payoutSchedule`:
```solidity
    function setPayoutScheduleFixed(
        uint256[] calldata _payoutSchedule,
        address _payoutTokenAddress
    ) external onlyOpenQ {
        require(
            bountyType == OpenQDefinitions.TIERED_FIXED,
            Errors.NOT_A_FIXED_TIERED_BOUNTY
        );
        payoutSchedule = _payoutSchedule;
        payoutTokenAddress = _payoutTokenAddress;

        // Resize metadata arrays and copy current members to new array
        // NOTE: If resizing to fewer tiers than previously, the final indexes will be removed
        string[] memory newTierWinners = new string[](payoutSchedule.length);
        bool[] memory newInvoiceComplete = new bool[](payoutSchedule.length);
        bool[] memory newSupportingDocumentsCompleted = new bool[](
            payoutSchedule.length
        );

        for (uint256 i = 0; i < tierWinners.length; i++) {
            newTierWinners[i] = tierWinners[i];
        }
        tierWinners = newTierWinners;

        for (uint256 i = 0; i < invoiceComplete.length; i++) {
            newInvoiceComplete[i] = invoiceComplete[i];
        }
        invoiceComplete = newInvoiceComplete;

        for (uint256 i = 0; i < supportingDocumentsComplete.length; i++) {
            newSupportingDocumentsCompleted[i] = supportingDocumentsComplete[i];
        }
        supportingDocumentsComplete = newSupportingDocumentsCompleted;
    }
```
However, I think it is actually impossible to reduce the size because it will incur an index out of bounds.
For example:
Let's say the old `payoutSchedule.length` is 5. You want to change that to 3.
It initializers new array with a size of 3:
`string[] memory newTierWinners = new string[](payoutSchedule.length);`
but it iterates over the old array, meaning 5 iterations will happen:
```solidity
 for (uint256 i = 0; i < tierWinners.length; i++) {
      newTierWinners[i] = tierWinners[i];
  }
```
When it will try to access index > 2 on `newTierWinners`, a runtime error will happen because the new array does not have enough space.

Tested with a simplified version:

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.17;

contract TieredFixedBountyV1 {

    constructor(uint256 payoutScheduleLength) {
        invoiceComplete = new bool[](payoutScheduleLength);
    }

    function setPayoutScheduleFixed(uint256 payoutScheduleLength) external {
        // Resize metadata arrays and copy current members to new array
        // NOTE: If resizing to fewer tiers than previously, the final indexes will be removed
        bool[] memory newInvoiceComplete = new bool[](payoutScheduleLength);

        for (uint256 i = 0; i < invoiceComplete.length; i++) {
            newInvoiceComplete[i] = invoiceComplete[i];
        }
        invoiceComplete = newInvoiceComplete;
    }
}
```

Steps:
1) Deploy the contract with an initial size, e.g. 5.
2) Try to call `setPayoutScheduleFixed` with a smaller number, e.g. 3, and see it reverts.

## Impact

The number of tiers cannot decrease once set. Only the same or higher number of values can be set.

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredFixedBountyV1.sol#L135-L171

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredPercentageBountyV1.sol#L138-L179

## Tool used

Manual Review

## Recommendation
Consider handling the case when the new array is smaller than the previous one.
