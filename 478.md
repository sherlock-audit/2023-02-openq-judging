GimelSec

high

# `BountyCore.receiveFunds` only checks `_volume != 0` when `_tokenAddress == address(0)`. Malicious users can create many deposits without depositing any funds.

## Summary

`BountyCore.receiveFunds` checks `_volume != 0` to prevent deposits with zero volume. However, if `_tokenAddress == address(0)` the volume is actually `msg.value` instead of `_volume`. A malicious user can call `DepositManagerV1.fundBountyToken` and use the wrong `_volume` to bypass the check.  

Once the check can be bypassed. Malicious users can create many deposits with zero volume. The size of `deposits[]` could be too big. Thus, nobody is able to refund due to the gas limit DoS of `bounty.getLockedFunds(depToken)` in `refundDeposit()`.

## Vulnerability Detail

`BountyCore.receiveFunds` only checks `_volume != 0`.
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L21
```solidity
    function receiveFunds(
        address _funder,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration
    )
        external
        payable
        virtual
        onlyDepositManager
        nonReentrant
        returns (bytes32, uint256)
    {
        require(_volume != 0, Errors.ZERO_VOLUME_SENT);
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        require(status == OpenQDefinitions.OPEN, Errors.CONTRACT_IS_CLOSED);

        bytes32 depositId = _generateDepositId();

        uint256 volumeReceived;
        if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        …
    }
```

A malicious user can call `DepositManagerV1.fundBountyToken` and use the wrong `_volume` to bypass the check. 

```solidity
DepositManagerV1.fundBountyToken(_bountyAddress, address(0), 100, 100, uuid);
```
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L54
```solidity
    function fundBountyToken(
        address _bountyAddress,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration,
        string memory funderUuid
    ) external payable onlyProxy {
        IBounty bounty = IBounty(payable(_bountyAddress));

        if (!isWhitelisted(_tokenAddress)) {
            require(
                !tokenAddressLimitReached(_bountyAddress),
                Errors.TOO_MANY_TOKEN_ADDRESSES
            );
        }

        require(bountyIsOpen(_bountyAddress), Errors.CONTRACT_ALREADY_CLOSED);

        (bytes32 depositId, uint256 volumeReceived) = bounty.receiveFunds{
            value: msg.value
        }(msg.sender, _tokenAddress, _volume, _expiration);

        …
```

Thus, a malicious user can create many deposits with zero volume. The size of `deposits[]` could be too big. `BountyCore.getLockedFunds` will always revert.
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L341
```solidity
    function getLockedFunds(address _depositId)
        public
        view
        virtual
        returns (uint256)
    {
        uint256 lockedFunds;
        bytes32[] memory depList = this.getDeposits();
        for (uint256 i = 0; i < depList.length; i++) {
            if (
                block.timestamp <
                depositTime[depList[i]] + expiration[depList[i]] &&
                tokenAddress[depList[i]] == _depositId
            ) {
                lockedFunds += volume[depList[i]];
            }
        }

        return lockedFunds;
    }
```

If `BountyCore.getLockedFunds` is DOSed. No one can call `DepositManagerV1.refundDeposit` to refund their deposits
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L172
```solidity
    function refundDeposit(address _bountyAddress, bytes32 _depositId)
        external
        onlyProxy
    {
        …

        uint256 availableFunds = bounty.getTokenBalance(depToken) -
            bounty.getLockedFunds(depToken);

        …

```

## Impact

Malicious users can create many deposits with zero volume to DOS `BountyCore.getLockedFunds`. If `BountyCore.getLockedFunds` is DOSed. No one can call `DepositManagerV1.refundDeposit` to refund their deposits. The funds could be stuck in the bounty.

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L21
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L341
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L54
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L172


## Tool used

Manual Review

## Recommendation

 if `_tokenAddress == address(0)`, `BountyCore.receiveFunds` should ensure `msg.value != 0`.
