GimelSec

high

# A refunded NFT could block `ClaimManagerV1.claimBounty`

## Summary

In `ClaimManagerV1.claimBounty()`, if the type of the bounty is `OpenQDefinitions.ATOMIC`, it would call `_claimAtomicBounty()`. Then, `_claimAtomicBounty` would claim all the `nftDeposits`. However, if one of the ntfDeposits is refunded. `_bounty.claimNft(_closer, _bounty.nftDeposits(i));` will always revert.
The same issue lies in `TieredFixedBountyV1` and `TieredPercentageBountyV1` if a malicious user has funded NFTs for all tiers and has refunded these NFTs back.

## Vulnerability Detail

`ClaimManagerV1._claimAtomicBounty` would claim all the `nftDeposits`.
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L151
```solidity

    function _claimAtomicBounty(
        IAtomicBounty _bounty,
        address _closer,
        bytes calldata _closerData
    ) internal {
       …

        for (uint256 i = 0; i < _bounty.getNftDeposits().length; i++) {
            _bounty.claimNft(_closer, _bounty.nftDeposits(i));

            emit NFTClaimed(
                _bounty.bountyId(),
                address(_bounty),
                _bounty.organization(),
                _closer,
                block.timestamp,
                _bounty.tokenAddress(_bounty.nftDeposits(i)),
                _bounty.tokenId(_bounty.nftDeposits(i)),
                _bounty.bountyType(),
                _closerData,
                VERSION_1
            );
        }
    }
```

If one of the nftDeposits is refunded. `_bounty.claimNft` will revert.
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L125
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L263
```solidity
    function claimNft(address _payoutAddress, bytes32 _depositId)
        external
        virtual
        onlyClaimManager
        nonReentrant
    {
        _transferNft(
            tokenAddress[_depositId],
            _payoutAddress,
            tokenId[_depositId]
        );
    }

    function _transferNft(
        address _tokenAddress,
        address _payoutAddress,
        uint256 _tokenId
    ) internal virtual {
        IERC721Upgradeable nft = IERC721Upgradeable(_tokenAddress);
        nft.safeTransferFrom(address(this), _payoutAddress, _tokenId); // This will revert if the nft is refunded
    }
```

## Impact

A malicious user can deposit a NFT into an atomic bounty, then refund it. Thus, all the funds in the bounty cannot be claimed since  `_bounty.claimNft` always reverts.

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L31
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L151
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L125
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L263
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L64


## Tool used

Manual Review

## Recommendation

Check `_bounty.refunded()` before claiming the NFTs.

```solidity
    function _claimAtomicBounty(
        IAtomicBounty _bounty,
        address _closer,
        bytes calldata _closerData
    ) internal {
       …

        for (uint256 i = 0; i < _bounty.getNftDeposits().length; i++) {
            if(!_bounty.refunded(_bounty.nftDeposits(i))) {
                _bounty.claimNft(_closer, _bounty.nftDeposits(i));
                …
            }
        }
}
```
