Ruhum

medium

# OngoingBountyV1 doesn't verify that `claimId` was unused

## Summary
Each claim is given an ID: `keccak256(claimant, claimantAsset)`. After a claim is executed, it's marked as used but there are no checks preventing from the same `claimId` being used multiple times
## Vulnerability Detail
Neither in the ClaimManagerV1 nor in the OngoingBountyV1 contract is a check to verify that a `claimId` is unused. Thus, you're able to execute the same claim multiple times as long as you get the oracle to send the tx.

## Impact
OngoingBountyV1 claims could be used multiple times to steal bounties funds.

## Code Snippet
There are no checks whether the `claimId` was already used: [_claimOngoingBounty()](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L173) & [_eligibleToClaimOngoingBounty()](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L456)
```sol
    /// @notice Claim method for OngoingBounty
    /// @param _bounty The payout address of the bounty
    /// @param _closer The payout address of the claimant
    /// @param _closerData ABI Encoded data associated with this claim
    /// @dev see IBountyCore.claimOngoingPayout.(_closerData) for _closerData ABI encoding schema
    function _claimOngoingBounty(
        IOngoingBounty _bounty,
        address _closer,
        bytes calldata _closerData
    ) internal {
        _eligibleToClaimOngoingBounty(_bounty, _closer, _closerData);

        (address tokenAddress, uint256 volume) = _bounty.claimOngoingPayout(
            _closer,
            _closerData
        );

        emit TokenBalanceClaimed(
            _bounty.bountyId(),
            address(_bounty),
            _bounty.organization(),
            _closer,
            block.timestamp,
            tokenAddress,
            volume,
            _bounty.bountyType(),
            _closerData,
            VERSION_1
        );
    }
    
    /// @notice Runs all require statements to determine if the claimant can claim an ongoing bounty payout
    function _eligibleToClaimOngoingBounty(
        IOngoingBounty bounty,
        address _closer,
        bytes memory _closerData
    ) internal view {
        require(
            bounty.status() == OpenQDefinitions.OPEN,
            Errors.CONTRACT_IS_NOT_CLAIMABLE
        );

        (, string memory claimant, , string memory claimantAsset) = abi.decode(
            _closerData,
            (address, string, address, string)
        );

        bytes32 claimId = bounty.generateClaimId(claimant, claimantAsset);

        if (bounty.invoiceRequired()) {
            require(
                bounty.invoiceComplete(claimId),
                Errors.INVOICE_NOT_COMPLETE
            );
        }

        if (bounty.supportingDocumentsRequired()) {
            require(
                bounty.supportingDocumentsComplete(claimId),
                Errors.SUPPORTING_DOCS_NOT_COMPLETE
            );
        }

        if (bounty.kycRequired()) {
            require(hasKYC(_closer), Errors.ADDRESS_LACKS_KYC);
        }
    }
```

Neither is there one on [claimOngoingPayout()](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/OngoingBountyV1.sol#L96):
```sol
    /// @notice Transfers a payout amount of an ongoing bounty to claimant for claimant asset
    /// @param _payoutAddress The destination address for the funds
    /// @param _closerData ABI-encoded data of the claimant and claimant asset
    /// @dev see IBountyCore.claimOngoingPayout.(_closerData) for _closerData ABI encoding schema
    function claimOngoingPayout(
        address _payoutAddress,
        bytes calldata _closerData
    ) external onlyClaimManager nonReentrant returns (address, uint256) {
        (, string memory claimant, , string memory claimantAsset) = abi.decode(
            _closerData,
            (address, string, address, string)
        );

        bytes32 _claimId = generateClaimId(claimant, claimantAsset);

        claimId[_claimId] = true;
        claimIds.push(_claimId);

        _transferToken(payoutTokenAddress, payoutVolume, _payoutAddress);
        return (payoutTokenAddress, payoutVolume);
    }
```

## Tool used

Manual Review

## Recommendation
Add the following check to `claimOngoingPayout()`: `require(!claimId[_claimId]);`
