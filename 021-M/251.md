unforgiven

high

# attacker can cause claim calls to bounty type TieredPercentageBounty to revert always by refunding his deposit after closeCompetition() get called

## Summary
Function `_claimTieredPercentageBounty()` is claim method for TieredPercentageBounty and it calls `claimTiered()` which calculates winner prize based on the `payoutSchedule[tier]` and `fundingTotals[token]` but because `fundingTotals[token]` is based on contract token balance when `closeCompetition()` is get called so if after call to `closeCompetition()` attacker refunds his funds and token balance becomes 0 then contract would try to transfer more than contract token balance and the whole claim transaction would revert.

## Vulnerability Detail
When function `_claimTieredPercentageBounty()` is get called for the first time it calls `bounty.closeCompetition()` to close the bounty. This is `closeCompetition()` code in TieredPercentageBounty:
```solidity
    /// @notice Similar to close() for single priced bounties. closeCompetition() freezes the current funds for the competition.
    function closeCompetition() external onlyClaimManager {
        require(
            status == OpenQDefinitions.OPEN,
            Errors.CONTRACT_ALREADY_CLOSED
        );

        status = OpenQDefinitions.CLOSED;
        bountyClosedTime = block.timestamp;

        for (uint256 i = 0; i < getTokenAddresses().length; i++) {
            address _tokenAddress = getTokenAddresses()[i];
            fundingTotals[_tokenAddress] = getTokenBalance(_tokenAddress);
        }
    }
```
As you can see it loops through bounty's deposited tokens and set the value of the `fundingTotals[token]` to contract token balance in that moment.
This is function `_claimTieredPercentageBounty()` code:
```solidity
   function _claimTieredPercentageBounty(
        IBounty _bounty,
        address _closer,
        bytes calldata _closerData
    ) internal {
        (, , , , uint256 _tier) = abi.decode(
            _closerData,
            (address, string, address, string, uint256)
        );

        _eligibleToClaimTier(_bounty, _tier, _closer);

        if (_bounty.status() == 0) {
            _bounty.closeCompetition();
................
        }

        for (uint256 i = 0; i < _bounty.getTokenAddresses().length; i++) {
            uint256 volume = _bounty.claimTiered(
                _closer,
                _tier,
                _bounty.getTokenAddresses()[i]
            );
...............
        }

        for (uint256 i = 0; i < _bounty.getNftDeposits().length; i++) {
            bytes32 _depositId = _bounty.nftDeposits(i);
            if (_bounty.tier(_depositId) == _tier) {
                _bounty.claimNft(_closer, _depositId);
..............
            }
        }
        _bounty.setTierClaimed(_tier);
    }
```
As you can see code loops through bounty deposit tokens and calls `bounty.claimTiered()`. This `claimTiered()` code in TieredPercentageBounty:
```solidity
    function claimTiered(
        address _payoutAddress,
        uint256 _tier,
        address _tokenAddress
    ) external onlyClaimManager nonReentrant returns (uint256) {
        require(
            bountyType == OpenQDefinitions.TIERED_PERCENTAGE,
            Errors.NOT_A_TIERED_BOUNTY
        );
        require(!tierClaimed[_tier], Errors.TIER_ALREADY_CLAIMED);

        uint256 claimedBalance = (payoutSchedule[_tier] *
            fundingTotals[_tokenAddress]) / 100;

        _transferToken(_tokenAddress, claimedBalance, _payoutAddress);
        return claimedBalance;
    }
```
As you can see it calculates the winner prize based on `fundingTotals[]` and tier share of the total reward. the issue is that the value of the `fundingTotals[token]` shows contract token balance when function `closeCompetition()` is get called. after that moment because of the refunds contract token balance can change and it would cause last users claims to revert because of the token shortage as tokens are refunded and there is not enough balance to pay prize for all users based on the `fundingTotals[]`. the issue would happen even if refunds happens for one deposited token and it would cause whole claim to revert. attacker can cause this issue intentionally for any TieredPercentageBounty and make users to lose funds by performing this steps:
1. User1 create Bounty1 as Tiered Percentage Bounty.
2. User1 deposits 100K USDC to Bounty1 for 3 month and set tier1 prize as 50% and tier2 prize as 50%.
3. attacker deposits 100 Token1 (which is in whitelist) into Bounty1 for 2 days.
4. after 3 month User1 set the winners as User2 and User3 for tier1 and tier2.
5. User2 calls `permissionedClaimTieredBounty()` to get his win prize and code would check the conditions and then would call `_claimTieredPercentageBounty()` which first call `Bounty1.closeCompetition()`.
6. function `closeCompetition()` would set `fundingTotals[USDC] = 100K` and `fundingTotals[Token1] = 100`.
7. then `_claimTieredPercentageBounty()` would transfer 50K USDC and 5 Token1 to User2 by calling `claimTiered()`.
8. now attacker would call `refundDeposit()` to withdraw his deposit as it is expired. code would send 50 Token1 to attacker and contract Token1 balance would become 0.
9. now User2 prize can't be claimed because claim function would go through USDC and Token1 and call `claimTiered()` for them but for Token1 code would calculate 50 token as prize (percentage * fundingTotals = 50% * 100 = 50) but contract doesn't have that much tokens in the balance.
10. User2 can't claim his funds neither by pulling or pushing methods.

even if attacker doesn't perform this attack, the deposit refunds after function `closeCompetition()` execution would cause wrong reward distribution and some winners can't receive their funds because of inefficient token balance.

## Impact
some tier winner can't claim their funds neither by pooling or pushing mechanism if some deposits are refunding after bounty is closed. attacker can cause this intentionally for any TieredPercentageBounty.

## Code Snippet
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L215-L236

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredPercentageBountyV1.sol#L123-L136

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredPercentageBountyV1.sol#L104-L120

## Tool used
Manual Review

## Recommendation
prevent refunds for some times after closing the bounty so winner or oracle have enough time to claim their wins while contract token balance isn't get changed by refunds.