GimelSec

high

# A malicious user can block other users from calling `refundDeposit()`

## Summary

A malicious user can call `fundBountyToken()` with a large value of `_expiration`. It causes `refundDeposit()` to always be reverted when other users want to refund due to the overflow of `depositTime[depList[i]] + expiration[depList[i]]` in `getLockedFunds()`.

## Vulnerability Detail

In `BountyCore.getLockedFunds()`:

```solidity
    function getLockedFunds(address _depositId)
        public
        view
        virtual
        returns (uint256)
    {
        uint256 lockedFunds;
        bytes32[] memory depList = this.getDeposits();
        for (uint256 i = 0; i < depList.length; i++) {
            if (
                block.timestamp <
                depositTime[depList[i]] + expiration[depList[i]] &&
                tokenAddress[depList[i]] == _depositId
            ) {
                lockedFunds += volume[depList[i]];
            }
        }

        return lockedFunds;
    }
```

`getLockedFunds()` checks if `block.timestamp < depositTime[depList[i]] + expiration[depList[i]]`. If a funder set a large expiration (for example, `type(uint256).max`), the condition in `getLockedFunds()` will be reverted because `depositTime[depList[i]] + expiration[depList[i]]` will always overflow.

When users want to refund, they should call `DepositManagerV1.refundDeposit()`. The `refundDeposit()` calculates `availableFunds` by:

```solidity
    function refundDeposit(address _bountyAddress, bytes32 _depositId)
        external
        onlyProxy
    {
        ...

        uint256 availableFunds = bounty.getTokenBalance(depToken) -
            bounty.getLockedFunds(depToken);

        ...
    }
```

If an attacker calls `fundBountyToken()` with a large value of `_expiration`, the `getLockedFunds()` will always be reverted. The `refundDeposit()` will also always be reverted due to the overflow in `getLockedFunds()`.

## Impact

Nobody could call `refundDeposit()` to refund tokens. The vulnerability is easy to exploit, the attacker just needs to pay a little money (for example, 1 wei) for `fundBountyToken()` and the cheap gas fee of Polygon to exploit.

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L333-L352
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L171-L172

## Tool used

Manual Review

## Recommendation

Limit `expiration` to a valid range in `fundBountyToken()`, `extendDeposit()` and `fundBountyNFT()`. Or check if `depositTime + expiration` will overflow.
