GimelSec

high

# Should Check `claimId` in `OngoingBountyV1.claimOngoingPayout`

## Summary

`OngoingBounty` lets multiple users claim the funds. And `OngoingBountyV1.claimOngoingPayout` would set `claimId[_claimId]` to true. However, `claimId[_claimId]` is not checked when a user tries to claim. A user can claim the funds multiple times.

## Vulnerability Detail

`OngoingBountyV1.claimOngoingPayout` would set `claimId[_claimId]` to true.
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/OngoingBountyV1.sol#L107
```solidity
    function claimOngoingPayout(
        address _payoutAddress,
        bytes calldata _closerData
    ) external onlyClaimManager nonReentrant returns (address, uint256) {
        (, string memory claimant, , string memory claimantAsset) = abi.decode(
            _closerData,
            (address, string, address, string)
        );

        bytes32 _claimId = generateClaimId(claimant, claimantAsset);

        claimId[_claimId] = true;
        claimIds.push(_claimId);

        _transferToken(payoutTokenAddress, payoutVolume, _payoutAddress);
        return (payoutTokenAddress, payoutVolume);
    }
```

But ` claimId[_claimId]` is not checked in `claimOngoingPayout`. Thus, an eligible user can claim the funds multiple times.

## Impact

An eligible user of an ongoing bounty can claim the funds multiple times. The user can use this issue to claim all the funds of the bounty.

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L180
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/OngoingBountyV1.sol#L107


## Tool used

Manual Review

## Recommendation

Check `claimId` in `OngoingBountyV1.claimOngoingPayout`

```solidity
    function claimOngoingPayout(
        address _payoutAddress,
        bytes calldata _closerData
    ) external onlyClaimManager nonReentrant returns (address, uint256) {
        (, string memory claimant, , string memory claimantAsset) = abi.decode(
            _closerData,
            (address, string, address, string)
        );

        bytes32 _claimId = generateClaimId(claimant, claimantAsset);
        require(!claimId[_claimId]);

        claimId[_claimId] = true;
        claimIds.push(_claimId);

        _transferToken(payoutTokenAddress, payoutVolume, _payoutAddress);
        return (payoutTokenAddress, payoutVolume);
    }
```
