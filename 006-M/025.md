Bauer

medium

# If user specifies both the msg.value and the ERC20 token address, the protocol token will be lost

## Summary
The ```DepositManagerV1.fundBountyToken()``` method allows user to transfer protocol token or ERC20 from msg.sender to bounty address. If user pass both ERC20 token and msg.value in the parameters, the protocol will only record the number of tokens, and the native token will be lost.

## Vulnerability Detail
```solidity
function fundBountyToken(
        address _bountyAddress,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration,
        string memory funderUuid
    ) external payable onlyProxy {
        IBounty bounty = IBounty(payable(_bountyAddress));

        if (!isWhitelisted(_tokenAddress)) {
            require(
                !tokenAddressLimitReached(_bountyAddress),
                Errors.TOO_MANY_TOKEN_ADDRESSES
            );
        }

        require(bountyIsOpen(_bountyAddress), Errors.CONTRACT_ALREADY_CLOSED);

        (bytes32 depositId, uint256 volumeReceived) = bounty.receiveFunds{
            value: msg.value
        }(msg.sender, _tokenAddress, _volume, _expiration);

        bytes memory funderUuidBytes = abi.encode(funderUuid);

        emit TokenDepositReceived(
            depositId,
            _bountyAddress,
            bounty.bountyId(),
            bounty.organization(),
            _tokenAddress,
            block.timestamp,
            msg.sender,
            _expiration,
            volumeReceived,
            0,
            funderUuidBytes,
            VERSION_1
        );
    }
```
The ```DepositManagerV1.fundBountyToken()``` method allows user to transfer protocol token or ERC20 to bounty address. Any ERC20 token and protocol token are allowed. Inside the function the protocol will call ```receiveFunds()``` to transfer token.
```solidity
function receiveFunds(
        address _funder,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration
    )
        external
        payable
        virtual
        onlyDepositManager
        nonReentrant
        returns (bytes32, uint256)
    {
        require(_volume != 0, Errors.ZERO_VOLUME_SENT);
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        require(status == OpenQDefinitions.OPEN, Errors.CONTRACT_IS_CLOSED);

        bytes32 depositId = _generateDepositId();

        uint256 volumeReceived;
        if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }

        funder[depositId] = _funder;
        tokenAddress[depositId] = _tokenAddress;
        volume[depositId] = volumeReceived;
        depositTime[depositId] = block.timestamp;
        expiration[depositId] = _expiration;
        isNFT[depositId] = false;

        deposits.push(depositId);
        tokenAddresses.add(_tokenAddress);

        return (depositId, volumeReceived);
    }
```
If token address is address(0) the protocol will record the number of msg.value, otherwise it will record the number of token.
```solidity

if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }

```
However, if user passes both the msg.value and the ERC20 token, the protocol will only record the number of ERC20 tokens according to the code above. ```onlyOracle``` may transfer the protocol token to someone else by calling claimBounty().User will lost the protocol token.

## Impact
User will lost the protocol token.

## Code Snippet
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L21-L58

```solidity

if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }

```


## Tool used

Manual Review

## Recommendation
```solidity


       if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
	     if(msg.value != 0) {
	          _transferToken(address(0),msg.value,_funder)
	    }
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }
or 

       if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
	    require(msg.value != 0,"ERROR")
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }
```
