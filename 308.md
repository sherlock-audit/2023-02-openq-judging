cryptostellar5

medium

# User Funds can get locked due to missing Require() check in receiveFunds() function

## Summary

If a `msg.value` is sent along with a non zero `_tokenAddress` and `_volume` then User Funds can get locked due to missing Require() check in `receiveFunds()` function. 

## Vulnerability Detail

According to the logic of `receiveFunds()` function

```solidity
/// @param _tokenAddress The ERC20 token address (ZeroAddress if funding with protocol token)
```

According to the current logic, the function expects that if a non zero `_tokenAddress` is provided then `msg.value` won't be present. This assumption in this function is flawed.
If a `msg.value` is sent along with a non zero `_tokenAddress` and `_volume` then User Funds can get locked due to missing Require() check in `receiveFunds()` function. 


## POC

1. Alice provides a non-zero `_tokenAddress` address and a non-zero `_volume`
2. Along with this Alice also sends 10 protocol tokens. Hence, `msg.value = 10`
3. The [if condition](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L41-L42) fails and [else block](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L43-L45) is successfully executed.

```solidity
 if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }
```

5.  Alice loses the 10 protocol tokens sent along because there was no require check to prevent such a case.
   

## Recommendation

The else block should have an additional require check, to check that user has not accidently sent protocol tokens ( `msg.value == 0`)

```solidity
require (msg.value == 0);
```

## Impact

User Funds can get locked due to a missing require() check in the else block of `receiveFunds()`

## Code Snippet

```solidity
/// @notice Creates a deposit and transfers tokens from msg.sender to this contract
    /// @param _funder The funder's address
    /// @param _tokenAddress The ERC20 token address (ZeroAddress if funding with protocol token)
    /// @param _volume The volume of token to transfer
    /// @param _expiration The duration until the deposit becomes refundable
    /// @return (depositId, volumeReceived) Returns the deposit id and the amount transferred to bounty
    function receiveFunds(
        address _funder,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration
    )
        external
        payable
        virtual
        onlyDepositManager
        nonReentrant
        returns (bytes32, uint256)
    {
        require(_volume != 0, Errors.ZERO_VOLUME_SENT);
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        require(status == OpenQDefinitions.OPEN, Errors.CONTRACT_IS_CLOSED);

        bytes32 depositId = _generateDepositId();

        uint256 volumeReceived;
        if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }

        funder[depositId] = _funder;
        tokenAddress[depositId] = _tokenAddress;
        volume[depositId] = volumeReceived;
        depositTime[depositId] = block.timestamp;
        expiration[depositId] = _expiration;
        isNFT[depositId] = false;

        deposits.push(depositId);
        tokenAddresses.add(_tokenAddress);

        return (depositId, volumeReceived);
    }
```

## Tool used

Manual Review
