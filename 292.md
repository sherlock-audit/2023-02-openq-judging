unforgiven

medium

# call to setPayoutSchedule() and setPayoutScheduleFixed() would revert when bounty's issuer try to resize winners to fewer tiers

## Summary
tiered bounty issuer can call `OpenQV1.setPayoutSchedule()` and `OpenQV1.setPayoutScheduleFixed()` to reset the tier winner prize and states but because of a bug in the `TieredPercentageBountyV1.setPayoutSchedule()` and `TieredFixedBountyV1.setPayoutScheduleFixed()` issuer set new values that number of the tiers are lower than previous one so it won't be possible for issuer to change payout amounts and bounty contract can become useless and deposit funds won't be accessible for the locked time.

## Vulnerability Detail
This is `TieredPercentageBountyV1.setPayoutSchedule()` and `TieredFixedBountyV1.setPayoutScheduleFixed()`  code which are called when issuer wants to change the payout schedule:
```solidity
    function setPayoutSchedule(uint256[] calldata _payoutSchedule)
        external
        onlyOpenQ
    {
        require(
            bountyType == OpenQDefinitions.TIERED_PERCENTAGE,
            Errors.NOT_A_TIERED_BOUNTY
        );
        uint256 sum;
        for (uint256 i = 0; i < _payoutSchedule.length; i++) {
            sum += _payoutSchedule[i];
        }
        require(sum == 100, Errors.PAYOUT_SCHEDULE_MUST_ADD_TO_100);

        payoutSchedule = _payoutSchedule;

        // Resize metadata arrays and copy current members to new array
        // NOTE: If resizing to fewer tiers than previously, the final indexes will be removed
        string[] memory newTierWinners = new string[](payoutSchedule.length);
        bool[] memory newInvoiceComplete = new bool[](payoutSchedule.length);
        bool[] memory newSupportingDocumentsCompleted = new bool[](
            payoutSchedule.length
        );

        for (uint256 i = 0; i < tierWinners.length; i++) {
            newTierWinners[i] = tierWinners[i];
        }
        tierWinners = newTierWinners;

        for (uint256 i = 0; i < invoiceComplete.length; i++) {
            newInvoiceComplete[i] = invoiceComplete[i];
        }
        invoiceComplete = newInvoiceComplete;

        for (uint256 i = 0; i < supportingDocumentsComplete.length; i++) {
            newSupportingDocumentsCompleted[i] = supportingDocumentsComplete[i];
        }
        supportingDocumentsComplete = newSupportingDocumentsCompleted;
    }
    
        function setPayoutScheduleFixed(
        uint256[] calldata _payoutSchedule,
        address _payoutTokenAddress
    ) external onlyOpenQ {
        require(
            bountyType == OpenQDefinitions.TIERED_FIXED,
            Errors.NOT_A_FIXED_TIERED_BOUNTY
        );
        payoutSchedule = _payoutSchedule;
        payoutTokenAddress = _payoutTokenAddress;

        // Resize metadata arrays and copy current members to new array
        // NOTE: If resizing to fewer tiers than previously, the final indexes will be removed
        string[] memory newTierWinners = new string[](payoutSchedule.length);
        bool[] memory newInvoiceComplete = new bool[](payoutSchedule.length);
        bool[] memory newSupportingDocumentsCompleted = new bool[](
            payoutSchedule.length
        );

        for (uint256 i = 0; i < tierWinners.length; i++) {
            newTierWinners[i] = tierWinners[i];
        }
        tierWinners = newTierWinners;

        for (uint256 i = 0; i < invoiceComplete.length; i++) {
            newInvoiceComplete[i] = invoiceComplete[i];
        }
        invoiceComplete = newInvoiceComplete;

        for (uint256 i = 0; i < supportingDocumentsComplete.length; i++) {
            newSupportingDocumentsCompleted[i] = supportingDocumentsComplete[i];
        }
        supportingDocumentsComplete = newSupportingDocumentsCompleted;
    }
```
As you can see in both functions contracts loops through all the items in the old arrays and copy them to new arrays but if the new array length (which is equal to new `payoutSchedule.length`) was less than previous array list (which is equal to old `payoutSchedule.length`) then code would revert and issuer transaction would fail and issuer won't be able to change the payout schedule of the bounty.
these are the steps that make the issue happen:
1. UserA creates a fixed tiered Bounty1 and deposits 60K USDT into the Bounty1 and set tier 1, 2 and 3 to prize as 30K, 20K and 10K. 
2. after some time contest finishes and because of the lower participation only two User1 and User2 win the tier 1 and 2 prizes and there is no other winner.
3. UserA wants to change the payout schedule to make it only two tiers and pay 35K and 25K to tier 1 and 2 but calls to `setPayoutScheduleFixed()` would revert because of out of bound index access for arrays.
4. User1 would get 30K and User2 would get 20K and they don't receive the real amounts and the extra 10K would stuck in the bounty until expiration time.


## Impact
fund loss or wrong fund distribution or griefing can happen based on the situation. some winner can lose funds because new payout schedule can't be set in the bounty. funds would get distributed based on old payout schedule and some unused funds would lock in the contract until deposit expire time.

## Code Snippet
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredPercentageBountyV1.sol#L159-L178

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredFixedBountyV1.sol#L151-L170

## Tool used
Manual Review

## Recommendation
change loops range to make sure code won't use indexes larger than array's length. use `mint(oldLength, newLength)` for loop range.