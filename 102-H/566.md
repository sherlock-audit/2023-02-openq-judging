Aymen0909

high

# Risk of reentrancy attack in the `permissionedClaimTieredBounty` function

## Summary

Both functions `_claimTieredFixedBounty` and `_claimTieredPercentageBounty` allow reentrancy attacks as they don't follow the CEI standard (check-effect-interact) and thus a given tier can claim more payouts that it is intended to.

## Vulnerability Detail

To claim a tiered bounty payouts (either fixed or percentage) one of the functions `permissionedClaimTieredBounty` or `claimBounty` must be called, those two functions will call internally the functions `_claimTieredFixedBounty` or `_claimTieredPercentageBounty` for fixed tier and percentage tier respectivly.

The functions `_claimTieredFixedBounty` and `_claimTieredPercentageBounty` work in a similair way : 

* First they ckeck the claim eligibility by using the function `_eligibleToClaimTier` which verify that the tier was not already claimed and checks invoice, deocumentents and kyc.

* Then the functions transfers all ERC20 tokens payouts using `claimTieredFixed` function for fixed tier and `claimTiered` for percentage tier.

* The third step is to transfer NFT payouts to the tier if there is any.

* And finally the function update the tier claimed status to true by calling `setTierClaimed`


Because the two functions don't follow the CEI standard and only update the tier claimed status after the tokens transfer they both allow reentrancy attacks that will allow a given tier to claim more ERC20 tokens payouts than it was intended to.

The scenario for the attack is the following :

* The winning tier user creates a contract which calls the `permissionedClaimTieredBounty` function. The contract must contain the `OnERC721Received` callback to receive NFT, and the user will implement the follwing logic in the callback :

1- approve the tier bounty contract to transfer the NFT.

2- call the `permissionedClaimTieredBounty` function again.

* When the contract calls the `permissionedClaimTieredBounty` function for the first time the NFT payout will be transferred to the contract, and this will trigger the `OnERC721Received` callback which will reenter the `permissionedClaimTieredBounty` function.

* Because the tier claimed status was not updated yet the `_eligibleToClaimTier` will not revert and thus more ERC20 tokens payouts will be transferred to the tier.

* As the `OnERC721Received` callback approved the tier bounty contract to transfer the NFT, the call `_bounty.claimNft` will not revert as it is allowed to transfer the NFT from the contract to the contract again.

Thus the user has performed a reentrancy attack and collected more payouts than he was intended to.

Note this attack is only possible for the tiered bounties who use NFT for payouts.

## Impact

Both functions `_claimTieredFixedBounty` and `_claimTieredPercentageBounty` allow reentrancy attacks as they don't follow the CEI standard (check-effect-interact) and thus a given tier can claim more payouts that it is intended to.

## Code Snippet

The issue occurs because of the instances below : 

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L203-L272

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L278-L341

## Tool used

Manual Review

## Recommendation

To avoid this issue both functions `_claimTieredFixedBounty` and `_claimTieredPercentageBounty` must respect the CEI standard and they should update the claimed status just after the ERC20 tokens transfer.


The `_claimTieredFixedBounty` should be updated as follow : 

```solidity
function _claimTieredFixedBounty(
    IBounty _bounty,
    address _closer,
    bytes calldata _closerData
) internal {
    (, , , , uint256 _tier) = abi.decode(
        _closerData,
        (address, string, address, string, uint256)
    );

    _eligibleToClaimTier(_bounty, _tier, _closer);

    if (_bounty.status() == 0) {
        _bounty.closeCompetition();

        emit BountyClosed(
            _bounty.bountyId(),
            address(_bounty),
            _bounty.organization(),
            address(0),
            block.timestamp,
            _bounty.bountyType(),
            new bytes(0),
            VERSION_1
        );
    }

    uint256 volume = _bounty.claimTieredFixed(_closer, _tier);
    
    
    /** @audit
        update the claimed status here to avoid reentrancy attack with onerc721received callback
    */
    _bounty.setTierClaimed(_tier);

    emit TokenBalanceClaimed(
        _bounty.bountyId(),
        address(_bounty),
        _bounty.organization(),
        _closer,
        block.timestamp,
        _bounty.payoutTokenAddress(),
        volume,
        _bounty.bountyType(),
        _closerData,
        VERSION_1
    );

    for (uint256 i = 0; i < _bounty.getNftDeposits().length; i++) {
        bytes32 _depositId = _bounty.nftDeposits(i);
        if (_bounty.tier(_depositId) == _tier) {
            _bounty.claimNft(_closer, _depositId);

            emit NFTClaimed(
                _bounty.bountyId(),
                address(_bounty),
                _bounty.organization(),
                _closer,
                block.timestamp,
                _bounty.tokenAddress(_depositId),
                _bounty.tokenId(_depositId),
                _bounty.bountyType(),
                _closerData,
                VERSION_1
            );
        }
    }
}

```

And the same should be done for the `_claimTieredPercentageBounty` function.