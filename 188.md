ctf_sec

high

# Refund NFT via DepositManager failed to clean up the nftDeposits array, result in failure when claiming the reward from ClaimManager.sol

## Summary

Refund NFT via DepositManager failed to clean up the nftDeposits array, result in failure when claiming the reward from ClaimManager.sol for atomic bounty

## Vulnerability Detail

In the current implementation, the user can supplies a NFT to fund the bounty contract via DepositManager.sol

```solidity
function fundBountyNFT(
        address _bountyAddress,
        address _tokenAddress,
        uint256 _tokenId,
        uint256 _expiration,
        bytes calldata _data
    ) external onlyProxy {
        IBounty bounty = IBounty(payable(_bountyAddress));

        require(isWhitelisted(_tokenAddress), Errors.TOKEN_NOT_ACCEPTED);
        require(bountyIsOpen(_bountyAddress), Errors.CONTRACT_ALREADY_CLOSED);

        bytes32 depositId = bounty.receiveNft(
            msg.sender,
            _tokenAddress,
            _tokenId,
            _expiration,
            _data
        );
```

which calls:

```solidity
function receiveNft(
        address _sender,
        address _tokenAddress,
        uint256 _tokenId,
        uint256 _expiration,
        bytes calldata
    ) external onlyDepositManager nonReentrant returns (bytes32) {
        require(
            nftDeposits.length < nftDepositLimit,
            Errors.NFT_DEPOSIT_LIMIT_REACHED
        );
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        _receiveNft(_tokenAddress, _sender, _tokenId);

        bytes32 depositId = _generateDepositId();

        funder[depositId] = _sender;
        tokenAddress[depositId] = _tokenAddress;
        depositTime[depositId] = block.timestamp;
        tokenId[depositId] = _tokenId;
        expiration[depositId] = _expiration;
        isNFT[depositId] = true;

        deposits.push(depositId);
        nftDeposits.push(depositId);

        return depositId;
    }
```

The the depositId is pushed into the array NFTDeposit to keep track of the nftDeposits

Then if NFT supplies changes his mind and call the refundDeposit to claim the NFT back, which calls:

```solidity
bounty.refundDeposit(_depositId, msg.sender, volume);
```

from DepositManager.sol, which calls

```solidity
function refundDeposit(
        bytes32 _depositId,
        address _funder,
        uint256 _volume
    ) external virtual onlyDepositManager nonReentrant {
        require(!refunded[_depositId], Errors.DEPOSIT_ALREADY_REFUNDED);
        require(funder[_depositId] == _funder, Errors.CALLER_NOT_FUNDER);
        require(
            block.timestamp >= depositTime[_depositId] + expiration[_depositId],
            Errors.PREMATURE_REFUND_REQUEST
        );

        refunded[_depositId] = true;

        if (tokenAddress[_depositId] == address(0)) {
            _transferProtocolToken(funder[_depositId], _volume);
        } else if (isNFT[_depositId]) {
            _transferNft(
                tokenAddress[_depositId],
                funder[_depositId],
                tokenId[_depositId]
            );
        } else {
            _transferERC20(
                tokenAddress[_depositId],
                funder[_depositId],
                _volume
            );
        }
    }
```

If the deposit is NFT, NFT is refunded:

```solidity
_transferNft(
  tokenAddress[_depositId],
  funder[_depositId],
  tokenId[_depositId]
);
```

But when NFT is refunded, the code failed to clean up the nftDeposits array

Later when the developer that completes the task tries to claim the bounty reward via ClaimManager.sol

which calls:

```solidity
function claimBounty(
        address _bountyAddress,
        address _closer,
        bytes calldata _closerData
    ) external onlyOracle onlyProxy {
        IBounty bounty = IBounty(payable(_bountyAddress));
        uint256 _bountyType = bounty.bountyType();

        if (_bountyType == OpenQDefinitions.ATOMIC) {
            // Decode to ensure data meets closerData schema before emitting any events
            abi.decode(_closerData, (address, string, address, string));

            _claimAtomicBounty(bounty, _closer, _closerData);
            bounty.close(_closer, _closerData);

            emit BountyClosed(
                bounty.bountyId(),
                _bountyAddress,
                bounty.organization(),
                _closer,
                block.timestamp,
                bounty.bountyType(),
                _closerData,
                VERSION_1
            );
        }
```

which calls _claimAtomicBounty,

```solidity
/// @notice Claim method for AtomicBounty
    /// @param _bounty The payout address of the bounty
    /// @param _closer The payout address of the claimant
    /// @param _closerData ABI Encoded data associated with this claim
    /// @dev See IAtomicBounty
    function _claimAtomicBounty(
        IAtomicBounty _bounty,
        address _closer,
        bytes calldata _closerData
    ) internal {
        _eligibleToClaimAtomicBounty(_bounty, _closer);

        for (uint256 i = 0; i < _bounty.getTokenAddresses().length; i++) {
            uint256 volume = _bounty.claimBalance(
                _closer,
                _bounty.getTokenAddresses()[i]
            );

            emit TokenBalanceClaimed(
                _bounty.bountyId(),
                address(_bounty),
                _bounty.organization(),
                _closer,
                block.timestamp,
                _bounty.getTokenAddresses()[i],
                volume,
                _bounty.bountyType(),
                _closerData,
                VERSION_1
            );
        }

        for (uint256 i = 0; i < _bounty.getNftDeposits().length; i++) {
            _bounty.claimNft(_closer, _bounty.nftDeposits(i));

            emit NFTClaimed(
                _bounty.bountyId(),
                address(_bounty),
                _bounty.organization(),
                _closer,
                block.timestamp,
                _bounty.tokenAddress(_bounty.nftDeposits(i)),
                _bounty.tokenId(_bounty.nftDeposits(i)),
                _bounty.bountyType(),
                _closerData,
                VERSION_1
            );
        }
    }

```

which calls bounty.nftDeposits which calls

```solidity
/// @notice Transfers NFT from bounty address to _payoutAddress
    /// @param _payoutAddress The destination address for the NFT
    /// @param _depositId The payout address of the bounty
    function claimNft(address _payoutAddress, bytes32 _depositId)
        external
        virtual
        onlyClaimManager
        nonReentrant
    {
        _transferNft(
            tokenAddress[_depositId],
            _payoutAddress,
            tokenId[_depositId]
        );
    }
```

However, after the bounty funder refund the NFT, the contract no longer has the ownership of the NFT, calling _transferNFT will revert, would block the bounty claim transaction which even block the ERC20 token claim.

In fact, more than atomicBounty claim is impacted, the FixedTierBounty claim and FixedTierPercetangeBounty claim is also impacted as long as the code below executes:

```solidity
        for (uint256 i = 0; i < _bounty.getNftDeposits().length; i++) {
            bytes32 _depositId = _bounty.nftDeposits(i);
            if (_bounty.tier(_depositId) == _tier) {
                _bounty.claimNft(_closer, _depositId);

                emit NFTClaimed(
                    _bounty.bountyId(),
                    address(_bounty),
                    _bounty.organization(),
                    _closer,
                    block.timestamp,
                    _bounty.tokenAddress(_depositId),
                    _bounty.tokenId(_depositId),
                    _bounty.bountyType(),
                    _closerData,
                    VERSION_1
                );
            }
        }
```

## Impact

Failure when claiming the reward from ClaimManager.sol for atomic bounty

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L106-L148

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L43

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L117-L167

## Tool used

Manual Review

## Recommendation

We recommend the protocol clean up the nftDeposits array after the NFT is refunded to avoid such transaction failure.