ctf_sec

high

# The same claimed id can be used to claim ongoing bounty multiple times

## Summary

The same claimed id can be used to claim ongoing bounty multiple times

## Vulnerability Detail

When oracle claims the developerâ€™s ongoing bounty, the function is called

```solidity
/// @notice Claim method for OngoingBounty
/// @param _bounty The payout address of the bounty
/// @param _closer The payout address of the claimant
/// @param _closerData ABI Encoded data associated with this claim
/// @dev see IBountyCore.claimOngoingPayout.(_closerData) for _closerData ABI encoding schema
function _claimOngoingBounty(
    IOngoingBounty _bounty,
    address _closer,
    bytes calldata _closerData
) internal {
    _eligibleToClaimOngoingBounty(_bounty, _closer, _closerData);

    (address tokenAddress, uint256 volume) = _bounty.claimOngoingPayout(
        _closer,
        _closerData
    );

    emit TokenBalanceClaimed(
        _bounty.bountyId(),
        address(_bounty),
        _bounty.organization(),
        _closer,
        block.timestamp,
        tokenAddress,
        volume,
        _bounty.bountyType(),
        _closerData,
        VERSION_1
    );
}
```

Which calls:

```solidity
_eligibleToClaimOngoingBounty(_bounty, _closer, _closerData);
```

which calls:

```solidity
/// @notice Runs all require statements to determine if the claimant can claim an ongoing bounty payout
function _eligibleToClaimOngoingBounty(
  IOngoingBounty bounty,
  address _closer,
  bytes memory _closerData
) internal view {
  require(
      bounty.status() == OpenQDefinitions.OPEN,
      Errors.CONTRACT_IS_NOT_CLAIMABLE
  );

  (, string memory claimant, , string memory claimantAsset) = abi.decode(
      _closerData,
      (address, string, address, string)
  );

  bytes32 claimId = bounty.generateClaimId(claimant, claimantAsset);

  if (bounty.invoiceRequired()) {
      require(
          bounty.invoiceComplete(claimId),
          Errors.INVOICE_NOT_COMPLETE
      );
  }

  if (bounty.supportingDocumentsRequired()) {
      require(
          bounty.supportingDocumentsComplete(claimId),
          Errors.SUPPORTING_DOCS_NOT_COMPLETE
      );
  }

  if (bounty.kycRequired()) {
      require(hasKYC(_closer), Errors.ADDRESS_LACKS_KYC);
  }
}
```

The code above validates that KYC required, supported document, and invoice requirement is passed. 

Then claimBounty is called in onGoingBounty

```solidity
(address tokenAddress, uint256 volume) = _bounty.claimOngoingPayout(
    _closer,
    _closerData
);
```

which calls:

```solidity
/// @notice Transfers a payout amount of an ongoing bounty to claimant for claimant asset
/// @param _payoutAddress The destination address for the funds
/// @param _closerData ABI-encoded data of the claimant and claimant asset
/// @dev see IBountyCore.claimOngoingPayout.(_closerData) for _closerData ABI encoding schema
function claimOngoingPayout(
  address _payoutAddress,
  bytes calldata _closerData
) external onlyClaimManager nonReentrant returns (address, uint256) {
  (, string memory claimant, , string memory claimantAsset) = abi.decode(
      _closerData,
      (address, string, address, string)
  );

  bytes32 _claimId = generateClaimId(claimant, claimantAsset);

  claimId[_claimId] = true;
  claimIds.push(_claimId);

  _transferToken(payoutTokenAddress, payoutVolume, _payoutAddress);
  return (payoutTokenAddress, payoutVolume);
}
```

note the claimId is set to true, but the neither the code does not validate that the claimId is already used to claim the bounty. Then the same claimId can be used to claim the bounty multiple times.

## Impact

Fund loss because the same claim id should not be used to claim the bounty multiple times.

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/OngoingBountyV1.sol#L91-L113

## Tool used

Manual Review

## Recommendation

Add check to make sure if the claim id is used, revert the transaction.