Atarpara

high

# Locked Funded Bounty amount forever

## Summary
Anyone can fund bounty when it's open state. When anyone can fund bounty it's store on deposit array on bounty contract. It's increasing array their is no any size bound. After expiration funded user can get back their deposit amount but due to unbound array it's will run out of gas error and it's stuck forever in bounty contract.   

## Vulnerability Detail
Any bounty receive fund via `receiveFunds`.

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L21
```solidity
    function receiveFunds(
        address _funder,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration
    )
        external
        payable
        virtual
        onlyDepositManager
        nonReentrant
        returns (bytes32, uint256)
    {
        require(_volume != 0, Errors.ZERO_VOLUME_SENT);
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        require(status == OpenQDefinitions.OPEN, Errors.CONTRACT_IS_CLOSED);

        bytes32 depositId = _generateDepositId();

        uint256 volumeReceived;
        if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }

        funder[depositId] = _funder;
        tokenAddress[depositId] = _tokenAddress;
        volume[depositId] = volumeReceived;
        depositTime[depositId] = block.timestamp;
        expiration[depositId] = _expiration;
        isNFT[depositId] = false;

        deposits.push(depositId);                    //@audit increasing array
        tokenAddresses.add(_tokenAddress);

        return (depositId, volumeReceived);
    }

```

User can get back their deposit via `refundDeposit`.

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L152
```solidity
    function refundDeposit(address _bountyAddress, bytes32 _depositId)
        external
        onlyProxy
    {
        IBounty bounty = IBounty(payable(_bountyAddress));

        require(
            bounty.funder(_depositId) == msg.sender,
            Errors.CALLER_NOT_FUNDER
        );

        require(
            block.timestamp >=
                bounty.depositTime(_depositId) + bounty.expiration(_depositId),
            Errors.PREMATURE_REFUND_REQUEST
        );

        address depToken = bounty.tokenAddress(_depositId);

        uint256 availableFunds = bounty.getTokenBalance(depToken) -
            bounty.getLockedFunds(depToken);

        uint256 volume;
        if (bounty.volume(_depositId) <= availableFunds) {
            volume = bounty.volume(_depositId);
        } else {
            volume = availableFunds;
        }

        bounty.refundDeposit(_depositId, msg.sender, volume);

        emit DepositRefunded(
            _depositId,
            bounty.bountyId(),
            _bountyAddress,
            bounty.organization(),
            block.timestamp,
            bounty.tokenAddress(_depositId),
            volume,
            0,
            new bytes(0),
            VERSION_1
        );
    }
```

In `refundDeposit` calling `bounty.getLockedFunds(depToken)` for locked token amount

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L333
```solidity
    function getLockedFunds(address _depositId)
        public
        view
        virtual
        returns (uint256)
    {
        uint256 lockedFunds;
        bytes32[] memory depList = this.getDeposits();  //@audit  return whole deposit array
        for (uint256 i = 0; i < depList.length; i++) {
            if (
                block.timestamp <
                depositTime[depList[i]] + expiration[depList[i]] &&
                tokenAddress[depList[i]] == _depositId
            ) {
                lockedFunds += volume[depList[i]];
            }
        }

        return lockedFunds;
    }
}
```
Here `getDeposits()` return whole `deposit` array which is unbounded length. Also `for` loop using ~ 3 SLOAD operation for calculating `lockedFunds`. 
`1 SLOAD = 2600 gas`

Gas Calculation :

Max possible `SLOAD` on `1 block`  = 30M/2600 = ~11538

`getDeposits()` = ~ 3 SLOAD * depositarraylength (depositTime, expiration, tokenAddress) + depositarraylength * 1 SLOAD (load deposit array)

Which Means

3 SLOAD * depositarraylength + depositarraylength * 1 SLOAD = 11538 SLOAD

depositarraylength = 11538/4 =  2884 

If Deposit Array Length > 2884 then `refundDeposit` get always run out of gas error.

Exploit Scenario:  
1.  Alice create bounty with ETH
2.  Alice and some other contribute Millions worth ETH on this bounty
3.  Malicious User Decide lock this fund forever
4.  He fund this bounty ~2900 times with only 1 wei and increase deposit array length greater than 2884 
5. When alice call `refundDeposit` it will always run out of gas error

Malicious user cost only 2900 Wei to lock amount forever in this bounty

## Impact
Lock User fund forever into bounty contract

## Code Snippet
See above

## Tool used
Manual Review

## Recommendation
Change your refundDeposit logic or add minAmount to fund any bounty. Adding minimum amount which increase attacker cost.