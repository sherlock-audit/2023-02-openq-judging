unforgiven

medium

# Function refundDeposit() in BountyCore doesn't decrease deposit items and it can cause deposits to reach to the limit even so they are refunded

## Summary
code keep track of different ERC20 token deposits and NFT token deposits for a bounty and allow deposits when deposit count is below the certain threshold. but when users refund their deposits after expiration time code won't decrease the deposit counts and it would cause DOS as users interact with the contract.

## Vulnerability Detail
when users deposits ERC20 tokens into a bounty code add the token into the `tokenAddresses` set in the `receiveFunds()` function in the BountyCore contract:
```solidity
    function receiveFunds(
        address _funder,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration
    )
        external
        payable
        virtual
        onlyDepositManager
        nonReentrant
        returns (bytes32, uint256)
    {
        require(_volume != 0, Errors.ZERO_VOLUME_SENT);
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        require(status == OpenQDefinitions.OPEN, Errors.CONTRACT_IS_CLOSED);

        bytes32 depositId = _generateDepositId();

        uint256 volumeReceived;
        if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }

        funder[depositId] = _funder;
        tokenAddress[depositId] = _tokenAddress;
        volume[depositId] = volumeReceived;
        depositTime[depositId] = block.timestamp;
        expiration[depositId] = _expiration;
        isNFT[depositId] = false;

        deposits.push(depositId);
        tokenAddresses.add(_tokenAddress);

        return (depositId, volumeReceived);
    }
```
and code won't allow new deposits when token address reach a limit in the `fundBountyToken()` function of the DepositManagerV1:
```solidity
    function fundBountyToken(
        address _bountyAddress,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration,
        string memory funderUuid
    ) external payable onlyProxy {
        IBounty bounty = IBounty(payable(_bountyAddress));

        if (!isWhitelisted(_tokenAddress)) {
            require(
                !tokenAddressLimitReached(_bountyAddress),
                Errors.TOO_MANY_TOKEN_ADDRESSES
            );
        }

        require(bountyIsOpen(_bountyAddress), Errors.CONTRACT_ALREADY_CLOSED);

        (bytes32 depositId, uint256 volumeReceived) = bounty.receiveFunds{
            value: msg.value
        }(msg.sender, _tokenAddress, _volume, _expiration);
......
......
```
but when users deposits expire and they refund their deposits and token balance of the contract is 0, code don't remove those tokens from `tokenAddresses`. this would cause `tokenAddresses` to have extra items and it won't be possible to fund the bounty with real deposits.
This is `refundDeposit()`  function in the BountyCore:
```solidity
    function refundDeposit(
        bytes32 _depositId,
        address _funder,
        uint256 _volume
    ) external virtual onlyDepositManager nonReentrant {
        require(!refunded[_depositId], Errors.DEPOSIT_ALREADY_REFUNDED);
        require(funder[_depositId] == _funder, Errors.CALLER_NOT_FUNDER);
        require(
            block.timestamp >= depositTime[_depositId] + expiration[_depositId],
            Errors.PREMATURE_REFUND_REQUEST
        );

        refunded[_depositId] = true;

        if (tokenAddress[_depositId] == address(0)) {
            _transferProtocolToken(funder[_depositId], _volume);
        } else if (isNFT[_depositId]) {
            _transferNft(
                tokenAddress[_depositId],
                funder[_depositId],
                tokenId[_depositId]
            );
        } else {
            _transferERC20(
                tokenAddress[_depositId],
                funder[_depositId],
                _volume
            );
        }
```
As you can see it doesn't remove token from `tokenAddresses` when contract balance in that token reaches to 0 (there is no logic to remove tokens from `tokenAddresses`) and also code doesn't remove NFT deposit with user refunds his NFT. 
so if this scenario happens:
1. User1 creates a Bounty1 to be funded by the community.
2. whitelisted tokens are 100 and token limit for a bounty is 10.
3. community users create deposits in tokens Token1, Token2, .... Token10.
4. User1 decide to change the Bounty1 rewards and ask everyone to fund bounty with Token11 instead of Token10.
5. community users refund their Token10 deposits and wants to deposit Token11 into the Bounty but they can't because even so the bounty Token10 balance is 0 and all deposits in that token is refunded but contract didn't remove Token10 from `tokenAddresses` and Bounty1 has reached the deposit token limits.

The same issue can happen for NFT deposits as by default a bounty can have 5 NFT deposit and if users deposit NFT by mistake and then refund them, contract won't remove them from `nftDeposits[]` and because of the size check in the `receiveNft()` other users can't deposit new NFTs even so old deposits are refunded. This `receiveNft()` code in AtomicBountyV1 which checks `require(nftDeposits.length < nftDepositLimit)`.
```solidity
    function receiveNft(
        address _sender,
        address _tokenAddress,
        uint256 _tokenId,
        uint256 _expiration,
        bytes calldata
    ) external onlyDepositManager nonReentrant returns (bytes32) {
        require(
            nftDeposits.length < nftDepositLimit,
            Errors.NFT_DEPOSIT_LIMIT_REACHED
        );
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        _receiveNft(_tokenAddress, _sender, _tokenId);

        bytes32 depositId = _generateDepositId();

        funder[depositId] = _sender;
        tokenAddress[depositId] = _tokenAddress;
        depositTime[depositId] = block.timestamp;
        tokenId[depositId] = _tokenId;
        expiration[depositId] = _expiration;
        isNFT[depositId] = true;

        deposits.push(depositId);
        nftDeposits.push(depositId);

        return depositId;
    }
```

## Impact
Bounty contract would reach their maximum ERC20 token deposit and maximum NFT deposits even so they don't have too much looked deposits and code would consider refunded deposits when checking total deposit limit. it would cause DOS and griefing when users interact with bounty normally.

## Code Snippet
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L64-L93

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L54-L56

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L45-L50

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/AtomicBountyV1.sol#L132-L135

## Tool used
Manual Review

## Recommendation
when refunding ERC20 deposits, check that if that deposit token balance in the contract become 0 then remove it from `tokenAddresses`.
when refunding NFT deposits, remove it from NFT deposit list or create a locked NFT deposits to keep track of not refunded NFT deposits.