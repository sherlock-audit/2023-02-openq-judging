bin2chen

medium

# setPayoutScheduleFixed/setPayoutSchedule() can't resize to fewer tiers

## Summary
if the new PayoutSchedule length is less than the old PayoutSchedule length, will out-of-bounds , can't resize to fewer tiers

## Vulnerability Detail
If the tier needs to be adjusted, the issuer can call setPayoutScheduleFixed() to do so
The implementation code is as follows:
```solidity
    function setPayoutScheduleFixed(
        uint256[] calldata _payoutSchedule,
        address _payoutTokenAddress
    ) external onlyOpenQ {
        require(
            bountyType == OpenQDefinitions.TIERED_FIXED,
            Errors.NOT_A_FIXED_TIERED_BOUNTY
        );
        payoutSchedule = _payoutSchedule;
        payoutTokenAddress = _payoutTokenAddress;

        // Resize metadata arrays and copy current members to new array
        // NOTE: If resizing to fewer tiers than previously, the final indexes will be removed
        string[] memory newTierWinners = new string[](payoutSchedule.length);
        bool[] memory newInvoiceComplete = new bool[](payoutSchedule.length);
        bool[] memory newSupportingDocumentsCompleted = new bool[](
            payoutSchedule.length
        );

        for (uint256 i = 0; i < tierWinners.length; i++) {
            newTierWinners[i] = tierWinners[i];   //@audit <---------out-of-bounds when  new  array length < old array length 
        }
....
    }
```

There is a problem with the current implementation: if the new length is less than the old length, it will revert, should be out-of-bounds

The following test code shows that the new length is smaller and will appear out-of-bounds
add to TieredFixedBounty.test.js
```js
		it('less_payout_schedule', async () => {
			// ASSUME
			let initialPayoutSchedule = await tieredFixedContract.getPayoutSchedule();
			let payoutToString = initialPayoutSchedule.map(thing => thing.toString());
			expect(payoutToString[0]).to.equal('80');
			expect(payoutToString[1]).to.equal('20');

			//@audit <-----this place will revert, because new array length smaller
			await tieredFixedContract.setPayoutScheduleFixed([100], mockLink.address);

			// ASSERT
			let expectedPayoutSchedule = await tieredFixedContract.getPayoutSchedule();
			payoutToString = expectedPayoutSchedule.map(thing => thing.toString());
			expect(payoutToString[0]).to.equal('100');
		});	
```

```yarn test test/Bounty/TieredFixedBounty.test.js --grep less_payout_schedule```

Output:
```console
  1) TieredFixedBountyV1.sol
       setPayoutSchedule
         less_payout_schedule:
     Error: VM Exception while processing transaction: reverted with panic code 0x32 (Array accessed at an out-of-bounds or negative index)
    at TieredFixedBountyV1.setPayoutScheduleFixed (contracts/Bounty/Implementations/TieredFixedBountyV1.sol:162)
```


Note******:TieredFixedBountyV1.setPayoutScheduleFixed and TieredPercentageBountyV1.setPayoutSchedule has the same problem

## Impact

can't resize to fewer tiers

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredFixedBountyV1.sol#L157-L170

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredPercentageBountyV1.sol#L165-L179

## Tool used

Manual Review

## Recommendation

```solidity
    function setPayoutScheduleFixed(
        uint256[] calldata _payoutSchedule,
        address _payoutTokenAddress
    ) external onlyOpenQ {
....
-       for (uint256 i = 0; i < tierWinners.length; i++) {
+       for (uint256 i = 0; i < tierWinners.length && i < newTierWinners.length; i++) {
            newTierWinners[i] = tierWinners[i];
        }
        tierWinners = newTierWinners;

-        for (uint256 i = 0; i < invoiceComplete.length; i++) {
+        for (uint256 i = 0; i < invoiceComplete.length && i < newInvoiceComplete.length; i++) {
            newInvoiceComplete[i] = invoiceComplete[i];
        }
        invoiceComplete = newInvoiceComplete;

-        for (uint256 i = 0; i < supportingDocumentsComplete.length; i++) {
+        for (uint256 i = 0; i < supportingDocumentsComplete.length && i < newSupportingDocumentsCompleted.length; i++) {
            newSupportingDocumentsCompleted[i] = supportingDocumentsComplete[i];
        }
        supportingDocumentsComplete = newSupportingDocumentsCompleted;
    }
```