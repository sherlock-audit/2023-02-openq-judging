slowfi

medium

# Funds can get locked on the bounty contract

## Summary

The current `receiveFunds` implementation of the `BountyCore.sol`  contract allows users to send native token along with a token address which makes the native tokens fund get locked in the bounty contract. 

## Vulnerability Detail

In order to fund a bounty, a funder needs to call the `fundBountyToken` from the `DepositManagerV1` contract. The deposit is supposed to be two different forms: or native token or any other ERC20/ERC777 token.

However it is possible to send native token with a non zero address. If this happens the deposit is registered for the token sent and the native token is locked in the bounty contract. The funder cant claim the deposit and unless any other funder performs a deposit in native currency the closer will not receive the  funds.

### Proof Of Concept

Instructions to run:
1. Create and empty foundry project. 
2. Install the openq as a library with forge install <repoName>/openq . Or copy the project inside lib folder.
3. Install other dependencies as forge-std, openzeppelin and openzeppelin upgradable.
4. Create on the test folder the two contracts left below.
5. Run using: `forge test --match-test testLostFunds`

BountyTester .t.sol contract

```solidity
pragma solidity 0.8.17;

import "./deployer.t.sol";
import "forge-std/Test.sol";
//import "openq/Library/OpenQDefinitions.sol";

contract BountyTester is Deployer, Test {

    address user1 = vm.addr(0x02);
    address user2 = vm.addr(0x02222);
    string _issuerExternalUserId = "slowfi";
    string bountyId = "superId";

    function setUp() public {
        deployEnv();
    }

    function initializeAtomicBounty() internal returns(address bounty) {
        string memory organization = "hardDickCafe";
            
        bool _hasFundingGoal =  true;
        address _fundingToken = address(0);
        uint256 _fundingGoal = type(uint256).max;
        bool _invoiceRequired = false;
        bool _kycRequired = false;
        bool _supportingDocumentsRequired = false;

        bytes memory data = abi.encode(_hasFundingGoal, _fundingToken, _fundingGoal, _invoiceRequired, _kycRequired, _supportingDocumentsRequired, _issuerExternalUserId, _issuerExternalUserId, _issuerExternalUserId);

        OpenQDefinitions.InitOperation memory initOp = OpenQDefinitions.InitOperation(uint32(0), data);
        vm.prank(user1);
        bounty = openqv1.mintBounty(bountyId, organization, initOp);
    }

    function _generateDepositId(string memory _bountyId, uint256 length) internal view virtual returns (bytes32) {
        return keccak256(abi.encode(_bountyId, length));
    }

    function testLostFunds() public {
        address bounty = initializeAtomicBounty();
        vm.deal(user1, 10 ether);
        token.mint(user1, 10 ether);
        vm.startPrank(user1);
        token.approve( bounty, 10 ether ); 
        depositmanager.fundBountyToken{value: 10 ether}( bounty, address(token), 10 ether, 15, _issuerExternalUserId );
        vm.stopPrank();
        assertEq(bounty.balance, 10 ether);
        assertEq(token.balanceOf(bounty), 10 ether);

        bytes32 depositId = _generateDepositId( bountyId, 0 );
        assertEq( AtomicBountyV1(payable(bounty)).tokenAddress(depositId), address(token) );

        vm.prank(ORACLE_ADDRESS);
        claimmanager.claimBounty( bounty, user2, abi.encode( address(0),_issuerExternalUserId,address(0),_issuerExternalUserId ) );

        assertEq(bounty.balance, 10 ether);
        assertEq(token.balanceOf(user2), 10 ether);
    }
}
```

deployer.t.sol contract
```solidity
pragma solidity 0.8.17;

import "openq/OpenQ/Implementations/OpenQV1.sol";
import "openq/OpenQ/Proxy/OpenQProxy.sol";
import "openq/ClaimManager/Implementations/ClaimManagerV1.sol";
import "openq/DepositManager/Implementations/DepositManagerV1.sol";
import "openq/TokenWhitelist/OpenQTokenWhitelist.sol";
import "openq/Bounty/Implementations/AtomicBountyV1.sol";
import "openq/Bounty/Implementations/OngoingBountyV1.sol";
import "openq/Bounty/Implementations/TieredPercentageBountyV1.sol";
import "openq/Bounty/Implementations/TieredFixedBountyV1.sol";
import "openq/Bounty/Proxy/BountyBeacon.sol";
import "openq/BountyFactory/BountyFactory.sol";
import "openq/Mocks/TestToken.sol";

abstract contract Deployer {

    address ORACLE_ADDRESS = address(0x01);

    OpenQV1 openqv1;
    OpenQV1 openqv1Residual;
    OpenQProxy openqproxy;

    ClaimManagerV1 claimmanager;
    OpenQProxy claimproxy;

    DepositManagerV1 depositmanager;
    OpenQProxy depositproxy;

    OpenQTokenWhitelist wtoken;

    AtomicBountyV1 atomicbounty;
    OngoingBountyV1 ongbounty;
    TieredPercentageBountyV1 tpbounty;
    TieredFixedBountyV1 tfbounty;

    BountyBeacon atomicbbeacon;
    BountyBeacon ongbbeacon;
    BountyBeacon tpbbeacon;
    BountyBeacon tfbbeacon;

    BountyFactory bfactory;

    TestToken token;
    TestToken token1;
    TestToken token2;
    TestToken token3;
    TestToken token4;
    TestToken token5;

    function deployEnv() public {
        openqv1 = new OpenQV1();
        openqv1Residual = openqv1;
        openqproxy = new OpenQProxy(address(openqv1), new bytes(0));
        openqv1 = OpenQV1( address(openqproxy) ); //Assign the proxy address to the OpenQV1 variable to have the ABI with the address
        openqv1.initialize();

        claimmanager = new ClaimManagerV1();
        claimproxy = new OpenQProxy(address(claimmanager), new bytes(0));
        claimmanager = ClaimManagerV1( address(claimproxy) );
        claimmanager.initialize( ORACLE_ADDRESS );

        depositmanager = new DepositManagerV1();
        depositproxy = new OpenQProxy(address(depositmanager), new bytes(0));
        depositmanager = DepositManagerV1( address(depositproxy));
        depositmanager.initialize();

        wtoken = new  OpenQTokenWhitelist(5);
        depositmanager.setTokenWhitelist(address(wtoken));

        atomicbounty = new AtomicBountyV1();
        ongbounty = new OngoingBountyV1();
        tpbounty = new TieredPercentageBountyV1();
        tfbounty = new TieredFixedBountyV1();

        atomicbbeacon = new BountyBeacon(address(atomicbounty));
        ongbbeacon = new BountyBeacon(address(ongbounty));
        tpbbeacon = new BountyBeacon(address(tpbounty));
        tfbbeacon = new BountyBeacon(address(tfbounty));

        bfactory = new BountyFactory(address(openqv1),address(atomicbbeacon),address(ongbbeacon),address(tpbbeacon),address(tfbbeacon));

        // Initialize
        openqv1.setBountyFactory(address(bfactory));
        openqv1.transferOracle(ORACLE_ADDRESS);
        openqv1.setDepositManager(address(depositmanager));
        openqv1.setClaimManager(address(claimmanager));
        claimmanager.setOpenQ( address(openqv1) );

        //Mock Tokens
        token = new TestToken("Token", "TK", 18);
        token1 = new TestToken("Token", "TK", 18);
        token2 = new TestToken("Token", "TK", 18);
        token3 = new TestToken("Token", "TK", 18);
        token4 = new TestToken("Token", "TK", 18);
        token5 = new TestToken("Token", "TK", 18);
    }

}
```

## Impact

Funders may loose funds and they can get locked on the bounty contract.

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L54-L56

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L41-L45

## Tool used

Manual Review and foundry for proof of concept.

## Recommendation

Check if there are native tokens along with a non zero token address and revert if so to avoid funds getting locked. Or implement a mechanism to allow both ways to be in a single deposit without locking the funds  in the bounty contract.

This check can be done on `DepositManagerV1.sol` contract or in `BountyCore.sol` contract. Ex on `DepositManagerV1`:

```solidity
    function fundBountyToken(
        address _bountyAddress,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration,
        string memory funderUuid
    ) external payable onlyProxy {
        IBounty bounty = IBounty(payable(_bountyAddress));

        if (!isWhitelisted(_tokenAddress)) {
            require(
                !tokenAddressLimitReached(_bountyAddress),
                Errors.TOO_MANY_TOKEN_ADDRESSES
            );
        }

        require(bountyIsOpen(_bountyAddress), Errors.CONTRACT_ALREADY_CLOSED);

		if ( msg.value !=0 && _tokenAddress !=0 ) {
			revert("Not possible to send native token and other token");
		}
		
        (bytes32 depositId, uint256 volumeReceived) = bounty.receiveFunds{
            value: msg.value
        }(msg.sender, _tokenAddress, _volume, _expiration);
```