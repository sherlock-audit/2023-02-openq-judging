0xmuxyz

high

# Due to lack of the validation to check whether or not a `claimId` is the `claimId` that is already claimed and completed a payout, which results in that same claimant can incorrectly receive the payout multiple times.

## Summary
Due to lack of the validation to check whether or not a `claimId` is the `claimId` that is already claimed and completed a payout, a bad situation will be caused that the OpenQ oracle can claim a payout of a claimant for the OngoingBountyV1 with the same claimId multiple times, which results in that the same claimant can incorrectly receive the payout multiple times. 

## Vulnerability Detail

The `claimId` is defined as a mapping storage and the `claimIds` is also defined as an array within the OngoingBountyStorage like this:
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Storage/OngoingBountyStorage.sol#L18-L22
```solidity
    /// @dev keccak256 hash of the claimant ID (GitHub ID) with the claimant asset ID (GitHub PR ID)
    mapping(bytes32 => bool) public claimId;

    // Keys of claims, can be used off-chain as an iterable to determine completed payouts
    bytes32[] public claimIds;
```

Within the ClaimManagerV1# `claimBounty()`, 
the ClaimManagerV1# `_claimOngoingBounty()` is called when the OpenQ oracle claim a payout of a claimant for OngoingBountyV1 like this:
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L56-L57
```solidity
    function claimBounty(
        address _bountyAddress,
        address _closer,
        bytes calldata _closerData
    ) external onlyOracle onlyProxy {
        IBounty bounty = IBounty(payable(_bountyAddress));
        uint256 _bountyType = bounty.bountyType();

        ...
        } else if (_bountyType == OpenQDefinitions.ONGOING) {
            _claimOngoingBounty(bounty, _closer, _closerData);   /// @audit
```

Then, within the ClaimManagerV1# `_claimOngoingBounty()`,
the ClaimManagerV1# `_eligibleToClaimOngoingBounty()` and the OngoingBountyV1# `claimOngoingPayout()` are called like this:
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L178-L183
```solidity
    function _claimOngoingBounty(
        IOngoingBounty _bounty,
        address _closer,
        bytes calldata _closerData
    ) internal {
        _eligibleToClaimOngoingBounty(_bounty, _closer, _closerData); /// @audit

        (address tokenAddress, uint256 volume) = _bounty.claimOngoingPayout(　/// @audit
            _closer,
            _closerData
        );
        ...
```

Within the ClaimManagerV1# `_eligibleToClaimOngoingBounty()`,
the claimant is checked whether or not the claimant is eligible for receiving an ongoing bounty payout like this:
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L456-L490
```solidity
    /// @notice Runs all require statements to determine if the claimant can claim an ongoing bounty payout
    function _eligibleToClaimOngoingBounty(
        IOngoingBounty bounty,
        address _closer,
        bytes memory _closerData
    ) internal view {
        require(
            bounty.status() == OpenQDefinitions.OPEN,
            Errors.CONTRACT_IS_NOT_CLAIMABLE
        );

        (, string memory claimant, , string memory claimantAsset) = abi.decode(
            _closerData,
            (address, string, address, string)
        );

        bytes32 claimId = bounty.generateClaimId(claimant, claimantAsset);

        if (bounty.invoiceRequired()) {
            require(
                bounty.invoiceComplete(claimId),
                Errors.INVOICE_NOT_COMPLETE
            );
        }

        if (bounty.supportingDocumentsRequired()) {
            require(
                bounty.supportingDocumentsComplete(claimId),
                Errors.SUPPORTING_DOCS_NOT_COMPLETE
            );
        }
        ...
    }
```

Within the OngoingBountyV1# `claimOngoingPayout()`,
`true` would be stored into the mapping storage of `claimId` and the `_claimId` generated via the `generateClaimId()` would be stored into the array of `claimIds` like this:
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/OngoingBountyV1.sol#L107-L108
```solidity
    function claimOngoingPayout(
        address _payoutAddress,
        bytes calldata _closerData
    ) external onlyClaimManager nonReentrant returns (address, uint256) {
        (, string memory claimant, , string memory claimantAsset) = abi.decode(
            _closerData,
            (address, string, address, string)
        );

        bytes32 _claimId = generateClaimId(claimant, claimantAsset);

        claimId[_claimId] = true;
        claimIds.push(_claimId);  /// @audit 

        _transferToken(payoutTokenAddress, payoutVolume, _payoutAddress); 
        ...
```
However, through the claiming process for a payout of the OngoingBountyV1 until above, there is no validation to check whether or not a `claimId` is the `claimId` that is already claimed and completed a payout. 
(NOTE： `_transferToken()` above and internal functions within the `_transferToken()` above also doesn't has the validation)

## Impact
This lead to a bad situation that the OpenQ oracle can claim a payout of a claimant for the OngoingBountyV1 with the same claimId multiple times, which results in that the same claimant can incorrectly receive the payout multiple times. 

## Code Snippet
- https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Storage/OngoingBountyStorage.sol#L18-L22
- https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L56-L57
- https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L178-L183
- https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L456-L490
- https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/OngoingBountyV1.sol#L107-L108


## Tool used
Manual Review

## Recommendation
Consider adding a validation to check whether or not a claimId is the claimId that is already claimed and completed a payout to the OngoingBountyV1# `claimOngoingPayout()` like this:
```solidity
    function claimOngoingPayout(
        address _payoutAddress,
        bytes calldata _closerData
    ) external onlyClaimManager nonReentrant returns (address, uint256) {
        (, string memory claimant, , string memory claimantAsset) = abi.decode(
            _closerData,
            (address, string, address, string)
        );

        bytes32 _claimId = generateClaimId(claimant, claimantAsset);

        /// @audit - Consider adding a validation to check whether or not a claimId is the claimId that is already claimed and completed a payout
        require(claimId[_claimId] == false, "This claimId is already claimed and completed a payout");

        claimId[_claimId] = true;
        claimIds.push(_claimId);
        
        _transferToken(payoutTokenAddress, payoutVolume, _payoutAddress);
        ...
```
