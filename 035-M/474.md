GimelSec

medium

# OpenQV1.initialize() doesn't call __Oraclize_init(_oracle)

## Summary

In `OpenQV1`, it doesn’t call `__Oraclize_init(_oracle)`.  It should initialize the oracle or the contract cannot function properly.

## Vulnerability Detail

`OpenQV1.initialize` doesn’t call `__Oraclize_init(_oracle)`.
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/OpenQ/Implementations/OpenQV1.sol#L14
```solidity
    function initialize() external initializer onlyProxy {
        __Ownable_init();
        __UUPSUpgradeable_init();
        __ReentrancyGuard_init();
    }
```

Functions with `onlyOracle` modifier are not working
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/OpenQ/Implementations/OpenQV1.sol#L464
```solidity
    function associateExternalIdToAddress(
        string calldata _externalUserId,
        address _associatedAddress
    ) external onlyOracle {
        // Clear previous addres<=>off-chain identity associations
        string memory formerExternalUserId = addressToExternalUserId[
            _associatedAddress
        ];
        address formerAddress = externalUserIdToAddress[_externalUserId];

        externalUserIdToAddress[formerExternalUserId] = address(0);
        addressToExternalUserId[formerAddress] = '';

        externalUserIdToAddress[_externalUserId] = _associatedAddress;
        addressToExternalUserId[_associatedAddress] = _externalUserId;

        emit ExternalUserIdAssociatedWithAddress(
            _externalUserId,
            _associatedAddress,
            formerExternalUserId,
            formerAddress,
            new bytes(0),
            VERSION_1
        );
    }
```

## Impact

If `oracle` is not set, functions with `onlyOracle` modifier are not working. But users can still call `mintBounty` to create bounties. But without calling `associateExternalIdToAddress`, the external users are not able to call `permissionedClaimTieredBounty` to claim the funds.

## Code Snippet

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Oracle/Oraclize.sol#L20
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/OpenQ/Storage/OpenQStorage.sol#L26
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/OpenQ/Implementations/OpenQV1.sol#L14

## Tool used

Manual Review

## Recommendation

Add `__Oraclize_init(_oracle)` in `OpenQV1.initialize`
```solidity
    function initialize(address _oracle) external initializer onlyProxy {
        __Ownable_init();
        __UUPSUpgradeable_init();
        __ReentrancyGuard_init();
        __Oraclize_init(_oracle);
    }
```
