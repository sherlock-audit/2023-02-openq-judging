unforgiven

high

# Wrong fund distribution when users refunds deposits  between claims and deposits set as refunded while code refund them partially

## Summary
users or issuer deposit their funds into a bounty and  funds would be locked for a duration and after the expiration time, users can withdraw their deposits. when users withdraw the their deposits, code calculates `availableFunds` to not pay the refunds from another users locked deposits. but this create and issue where if during the lock time, claims happen then any users who withdraw his deposits first would get a penalty and contract would pay the claims tokens only from his deposits and set their deposits as refunded even so they are refunded partially.

## Vulnerability Detail
This is function `refundDeposit()` code in OpenQV1 contract:
```solidity
    function refundDeposit(address _bountyAddress, bytes32 _depositId)
        external
        onlyProxy
    {
        IBounty bounty = IBounty(payable(_bountyAddress));

        require(
            bounty.funder(_depositId) == msg.sender,
            Errors.CALLER_NOT_FUNDER
        );

        require(
            block.timestamp >=
                bounty.depositTime(_depositId) + bounty.expiration(_depositId),
            Errors.PREMATURE_REFUND_REQUEST
        );

        address depToken = bounty.tokenAddress(_depositId);

        uint256 availableFunds = bounty.getTokenBalance(depToken) -
            bounty.getLockedFunds(depToken);

        uint256 volume;
        if (bounty.volume(_depositId) <= availableFunds) {
            volume = bounty.volume(_depositId);
        } else {
            volume = availableFunds;
        }

        bounty.refundDeposit(_depositId, msg.sender, volume);
........
    }
```
As can see code calculates `availableFunds` by calling `bounty.getLockedFunds()`. This is `getLockedFunds()` code:
```solidity
    function getLockedFunds(address _depositId)
        public
        view
        virtual
        returns (uint256)
    {
        uint256 lockedFunds;
        bytes32[] memory depList = this.getDeposits();
        for (uint256 i = 0; i < depList.length; i++) {
            if (
                block.timestamp <
                depositTime[depList[i]] + expiration[depList[i]] &&
                tokenAddress[depList[i]] == _depositId
            ) {
                lockedFunds += volume[depList[i]];
            }
        }

        return lockedFunds;
    }
```
As you can see the code returns the amount of locked tokens (of a specific token) on a bounty address that are not expired. so the `availableFunds` would depend on the contract current token balance(claims would change availableFunds) and current locked deposits. then in `refundDeposits()` function, code calculates `volume = min(availableFunds, bounty.volume(depositId)` and call `bounty.refundDeposit()` with `volume` amount. This is `bountyCore.refundDeposit()` code:
```solidity
    function refundDeposit(
        bytes32 _depositId,
        address _funder,
        uint256 _volume
    ) external virtual onlyDepositManager nonReentrant {
        require(!refunded[_depositId], Errors.DEPOSIT_ALREADY_REFUNDED);
        require(funder[_depositId] == _funder, Errors.CALLER_NOT_FUNDER);
        require(
            block.timestamp >= depositTime[_depositId] + expiration[_depositId],
            Errors.PREMATURE_REFUND_REQUEST
        );

        refunded[_depositId] = true;

        if (tokenAddress[_depositId] == address(0)) {
            _transferProtocolToken(funder[_depositId], _volume);
        } else if (isNFT[_depositId]) {
            _transferNft(
                tokenAddress[_depositId],
                funder[_depositId],
                tokenId[_depositId]
            );
        } else {
            _transferERC20(
                tokenAddress[_depositId],
                funder[_depositId],
                _volume
            );
        }
    }
```
As you can see code transfers `_volume` of the funds and then set `refunded[_depositId] = true`. so depositors may be partially refunded when users call `refundDeposit()` but code would still set those deposits as refunded. This can cause users to get wrong amount of funds and code would distribute funds wrongly and some depositors would lose their funds in favor of others.
This is the scenario that cause the issue:
1. User1 deposit 250 for 1 week.
2. User2 deposit 250 for 2 weeks.
3. User3 deposit 250 for 3 weeks.
4. after 10 days: Winner claim 250.
5. now if User1 calls refund he would receive nothing. (available funds are 0) but the deposit would be set as refunded.
6. if User2 calls refunds after expire time he would receive his 250 token(available funds are 250)
7. if User1 waits for User2 deposit to expire and then calls refund immediately User1 would receive 250 tokens instead of User2.
8. contract take the claim amount from random user based on the order of deposits expire times and order of refund calls.

As it's demonstrated in the above POC, the amount users receives depends on the time of the deposits expire times and current claimed amounts and order of the refund calls. so contract would distributes funds wrong if any claim happens before the refund calls or deposits expires before refund calls.

## Impact
some depositors would lose funds as they would get refunded partially but their deposits would be set as refunded they would lose funds in favor of the other depositors.

## Code Snippet
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/DepositManager/Implementations/DepositManagerV1.sol#L171-L179

https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L333-L352

## Tool used
Manual Review

## Recommendation
only set deposit as refunded if all of it is refunded and otherwise set the volume of the deposit.
the more fundamental solution is that to pay claim amounts from all the depositors based on ratio of their deposit amount and total deposit amounts.