Ruhum

high

# Refunding an NFT will result in claims being reverted

## Summary
When an NFT is refunded by a depositor, subsequent claims will revert. 

## Vulnerability Detail
After the NFT was refunded, the protocol doesn't clear the token from the `nftDeposits` array. When a user tries to claim their bounty, the transaction will revert because the contract will try to transfer the refunded NFT to the user. Since it doesn't own the token anymore, the transaction will fail. That will block both ERC20 and NFT rewards from being claimable.

## Impact
After an NFT was refunded nobody will be able to claim their reward in the AtomicBountyV1. In the case of TieredFixedBounty and TieredPercentageBounty the tier for which the NFT was deposited won't be claimable anymore.

## Code Snippet

When an NFT is deposited, it's added to the `nftDeposits` array, e.g. in [AtomicBounty](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/AtomicBountyV1.sol#L149) & [TieredBountyCore](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredBountyCore.sol#L45):
```sol
    function receiveNft(
        address _sender,
        address _tokenAddress,
        uint256 _tokenId,
        uint256 _expiration,
        bytes calldata
    ) external onlyDepositManager nonReentrant returns (bytes32) {
        require(
            nftDeposits.length < nftDepositLimit,
            Errors.NFT_DEPOSIT_LIMIT_REACHED
        );
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        _receiveNft(_tokenAddress, _sender, _tokenId);

        bytes32 depositId = _generateDepositId();

        funder[depositId] = _sender;
        tokenAddress[depositId] = _tokenAddress;
        depositTime[depositId] = block.timestamp;
        tokenId[depositId] = _tokenId;
        expiration[depositId] = _expiration;
        isNFT[depositId] = true;

        deposits.push(depositId);
        nftDeposits.push(depositId);

        return depositId;
    }
```

In [`BountyCore.refundDeposit()`](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L64-L93) the `nftDeposit` entry is not cleared after the refund:
```sol
    function refundDeposit(
        bytes32 _depositId,
        address _funder,
        uint256 _volume
    ) external virtual onlyDepositManager nonReentrant {
        require(!refunded[_depositId], Errors.DEPOSIT_ALREADY_REFUNDED);
        require(funder[_depositId] == _funder, Errors.CALLER_NOT_FUNDER);
        require(
            block.timestamp >= depositTime[_depositId] + expiration[_depositId],
            Errors.PREMATURE_REFUND_REQUEST
        );

        refunded[_depositId] = true;

        if (tokenAddress[_depositId] == address(0)) {
            _transferProtocolToken(funder[_depositId], _volume);
        } else if (isNFT[_depositId]) {
            _transferNft(
                tokenAddress[_depositId],
                funder[_depositId],
                tokenId[_depositId]
            );
        } else {
            _transferERC20(
                tokenAddress[_depositId],
                funder[_depositId],
                _volume
            );
        }
    }
```

When a user claims a bounty, the ClaimManager loops over all the NFT deposits to send those to the user as well. Since refunded NFTs are not removed from the array, it tries to claim those as well. That will result in the transaction being reverted:
[`_claimAtomicBounty()`](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#LL150-L165C10):
```sol
        for (uint256 i = 0; i < _bounty.getNftDeposits().length; i++) {
            _bounty.claimNft(_closer, _bounty.nftDeposits(i));

            emit NFTClaimed(
                _bounty.bountyId(),
                address(_bounty),
                _bounty.organization(),
                _closer,
                block.timestamp,
                _bounty.tokenAddress(_bounty.nftDeposits(i)),
                _bounty.tokenId(_bounty.nftDeposits(i)),
                _bounty.bountyType(),
                _closerData,
                VERSION_1
            );
        }
```

[`_claimTieredPercentageBounty()`](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L251):
```sol
        for (uint256 i = 0; i < _bounty.getNftDeposits().length; i++) {
            bytes32 _depositId = _bounty.nftDeposits(i);
            if (_bounty.tier(_depositId) == _tier) {
                _bounty.claimNft(_closer, _depositId);

                emit NFTClaimed(
                    _bounty.bountyId(),
                    address(_bounty),
                    _bounty.organization(),
                    _closer,
                    block.timestamp,
                    _bounty.tokenAddress(_depositId),
                    _bounty.tokenId(_depositId),
                    _bounty.bountyType(),
                    _closerData,
                    VERSION_1
                );
            }
        }
```

[`_claimTieredFixedBounty()`](https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/ClaimManager/Implementations/ClaimManagerV1.sol#L320):
```sol
        for (uint256 i = 0; i < _bounty.getNftDeposits().length; i++) {
            bytes32 _depositId = _bounty.nftDeposits(i);
            if (_bounty.tier(_depositId) == _tier) {
                _bounty.claimNft(_closer, _depositId);

                emit NFTClaimed(
                    _bounty.bountyId(),
                    address(_bounty),
                    _bounty.organization(),
                    _closer,
                    block.timestamp,
                    _bounty.tokenAddress(_depositId),
                    _bounty.tokenId(_depositId),
                    _bounty.bountyType(),
                    _closerData,
                    VERSION_1
                );
            }
        }
```
## Tool used

Manual Review

## Recommendation
After an NFT was refunded, it should be removed from the `nftDeposits` array.
