TrungOre

high

# Function `BountyCore.receiveFunds` forget checking `msg.value > 0` when `tokenAddress == address(0)`

## Summary
Attacker can call `BountyCore.receiveFunds()` with `_tokenAddress = address(0)`, `volume > 0` and `msg.value = 0` which can incur some issues for the users. 
 
## Vulnerability Detail
Function `BounttyCore.receiveFunds()` have a check whether `_volume > 0` to make sure no1 can call this function without contributing any tokens into the bounty. Unfortunately this check will be true with normal ERC20 tokens not with the native one since the implementation doesn't require the `msg.value == volume`. 
```solidity=
function receiveFunds(
        address _funder,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration
    )
        external
        payable
        virtual
        onlyDepositManager
        nonReentrant
        returns (bytes32, uint256)
    {
        require(_volume != 0, Errors.ZERO_VOLUME_SENT);
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        require(status == OpenQDefinitions.OPEN, Errors.CONTRACT_IS_CLOSED);

        bytes32 depositId = _generateDepositId();

        uint256 volumeReceived;
        if (_tokenAddress == address(0)) {
            volumeReceived = msg.value;
        } else {
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }

        funder[depositId] = _funder;
        tokenAddress[depositId] = _tokenAddress;
        volume[depositId] = volumeReceived;
        depositTime[depositId] = block.timestamp;
        expiration[depositId] = _expiration;
        isNFT[depositId] = false;

        deposits.push(depositId);
        tokenAddresses.add(_tokenAddress);

        return (depositId, volumeReceived);
    }
```
So users can call this function without paying anything by using the parameter as follows: 
* `msg.value = 0`
* `_volume = x > 0`
* `_tokenAddress = address(0)`

With this flaw, attacker can: 
1. execute a lot of deposits to make the `BountyCore.deposits[]` array grow massively. This will make the function `BountyCore.getLockedFunds()` out-of-gas to call which will incur the DDOS when funder try to call `DepositManagerV1.refundDeposit()` to claim their deposits back. 
    *  `BountyCore.getLockedFunds()` is out-of-gas because it loops through the entire `deposits[]` array which is very large 
    * `DepositManagerV1.refundDeposit()` is DDOS because it calls to function `BountyCore.getLockedFunds()` to get the total locked funds. 
3. If the `openQTokenWhitelist.TOKEN_ADDRESS_LIMIT() == 1`, attacker can front-run to take a slot of ETH in `BountyCore.tokenAddresses[]`. In case the issuer of bounty wants to fund the competitor with non-whitelisted token, (s)he can't do that because when he call `DepositManagerV1.fundBountyToken()` it will revert since the token addresses limit is reached. 

## Impact
* User can call refund to take their deposit back 
* Issuer can't pay the competitor with their expected tokens 

## Code Snippet
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L34
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L41-L42

## Tool used
Manual review 

## Recommendation
Modify `BountyCore.receiveFunds()` as follows: 
```solidity=
function receiveFunds(
        address _funder,
        address _tokenAddress,
        uint256 _volume,
        uint256 _expiration
    )
        external
        payable
        virtual
        onlyDepositManager
        nonReentrant
        returns (bytes32, uint256)
    {
        require(_volume != 0, Errors.ZERO_VOLUME_SENT);
        require(_expiration > 0, Errors.EXPIRATION_NOT_GREATER_THAN_ZERO);
        require(status == OpenQDefinitions.OPEN, Errors.CONTRACT_IS_CLOSED);

        bytes32 depositId = _generateDepositId();

        uint256 volumeReceived;
        if (_tokenAddress == address(0)) {
            /// change here 
            require(_volume == msg.value);
            volumeReceived = msg.value;
        } else {
            /// change here 
            require(msg.value == 0);
            volumeReceived = _receiveERC20(_tokenAddress, _funder, _volume);
        }

        funder[depositId] = _funder;
        tokenAddress[depositId] = _tokenAddress;
        volume[depositId] = volumeReceived;
        depositTime[depositId] = block.timestamp;
        expiration[depositId] = _expiration;
        isNFT[depositId] = false;

        deposits.push(depositId);
        tokenAddresses.add(_tokenAddress);

        return (depositId, volumeReceived);
    }
```